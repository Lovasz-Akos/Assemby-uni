code	segment para	public	'code'

assume cs:code, ds:code, es:nothing, ss:nothing

verzio	equ	 107			; Verzi¢sz m (1.07)

hndnum	macro
	hndver	%verzio
	endm

vernum	macro
	defver	%verzio
	endm

hndver	macro	vnum
	irpc	x,vnum
	db	'&x&'
	endm
	endm

defver	macro	vnum
num	=	0
	irpc	x,vnum
	db	'&x&'
	if	num eq 0
	db	'.'
	endif
num	=	num + 1
	endm
	endm

datum	macro	datstr

ev	=	0
honap	=	0
nap	=	0
cikl	=	0

	irpc	elem, datstr
	if	((cikl eq 1) or (cikl eq 2))
honap = (honap * 10) + elem
	endif
	if	((cikl eq 4) or (cikl eq 5))
nap   = (nap * 10) + elem
	endif
	if	((cikl eq 7) or (cikl eq 8))
ev    = (ev * 10) + elem
	endif
cikl = cikl + 1
	endm

datdef	record	evdef:6 = ev - 80, hodef:4 =honap ,napdef:5 = nap

	endm

	datum	%??date

	ifdef	sysdef
	dw	0ffffh,0ffffh		; Igy most SYS-nek megfelel”
	else
	jmp	start			; Ha COM, itt kezd”dj”n !
	nop				; Helyfoglal s (¡gy 4 byte !)
	endif

attrw	dw	800h			; Lemez ATTRIBUTUM sz¢
	dw	parelv			; Puffer-rutin c¡me
	dw	keruti			; Kezel” rutin c¡me
	db	1			; Egy egys‚get gener lunk

csekod	db	1			; Cserejelz”
bpbpoi	dw	bpbofs			; Pointer a BPB-re
bufcim	dw	0,0			; A parancsor c¡me
xmsrut	dw	0ffffh,0ffffh		; XMS rutin c¡mt r
jmppos	dw	0ffffh,0ffffh		; Ide mutat az offszet
extruc	dw	0,0			; XMS h¡v s helye
xmshnd	dw	0ffffh			; XMS handle (c¡m)
memlei	dw	0,0,0,0,0,0,0,0 	; Mem¢ria le¡r¢t bla
kodofs	db	0			; Ez a r”vid JMP offszetje

boots	label	word
	jmp	short $ 		; BOOT ugr s !
	nop				; BOOT NOP !
	db	'K. P‚ter'              ; Azonos¡t¢

bpbofs	label	word

	dw	512			; Ennyi byte-os a szektor
	db	0			; Ez a cluster-hossz
	dw	1			; Foglalt hossz
	db	1			; FAT sz m
	dw	64			; Bejegyz‚ssz m
	dw	0			; Szektorsz m
	db	0f8h			; T¡pusjelz”
	dw	1			; FAT hossz
	dw	1			; Egy track szektorsz ma
	dw	1			; Oldalsz m
	dw	0			; Rejtett szektorok sz ma
	db	8			; Hatv nysz m (2**8 = 512)

drvtip	db	2			; T¡pusjelz”

jmptab	dw	rdinit			; 0- s	INIT
	dw	csreke			; 1-es	Csere vizsg lat
	dw	bpbker			; 2-es	PBP k‚rdez‚s
	dw	egyhib			; 3-as	Hib ra egyb”l
	dw	secrea			; 4-es	Olvas s
	dw	sivime			; 5-”s	Vissza egyb”l
	dw	sivime			; 6-os	Vissza egyb”l
	dw	sivime			; 7-es	Vissza egyb”l
	dw	secwri			; 8-as	Ir s
	dw	secwri			; 9-es	Ir s + ™H
	dw	sivime			; 10-es Vissza egyb”l
	dw	sivime			; 11-es Vissza egyb”l
	dw	sivime			; 12-es Vissza egyb”l
	dw	sivime			; 13-as Vissza egyb”l
	dw	sivime			; 14-es Vissza egyb”l
	dw	opcser			; 15-”s ismertelen


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Parancspuffer elv‚tele. 			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
parelv:
	mov	cs:[bufcim],bx		; Ez az offszet
	mov	cs:[bufcim + 2],es	; Ez a paragrafusc¡m
	retf				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Eszk”zkezel” rutin.				;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
keruti:
	push	si			; SI ment‚s
	push	ax			; AX ment‚s
	push	cx			; CX ment‚s
	push	dx			; DX ment‚s
	push	di			; DI ment‚s
	push	bp			; BP ment‚s
	push	ds			; DS ment‚s
	push	es			; ES ment‚s
	push	bx			; BX ment‚s
	lds	bx,dword ptr cs:[bufcim]; DS:BX <- pufferc¡m
	mov	cx,[bx + 12h]		; CX <- 1. param‚ter
	mov	dx,[bx + 14h]		; DX <- 2. param‚ter
	mov	al,[bx + 2]		; Parancsk¢d
	cmp	al,15			; Nagyobb ann l ?
	ja	egyhib			; Igen, hibajelz‚sre
	mov	si,offset jmptab	; Itt van a t bl zat
	cbw				; AX <- 0000 + AL
	shl	ax,1			; Egy bejegyz‚s 2 byte
	add	si,ax			; Eltol s
	les	di,[bx + 0eh]		; ES:DI <- pufferc¡m
	push	cs			; Ebb”l lesz DS
	pop	ds			; DS <- CS
	jmp	[si]			; Ugr s a funkci¢ra

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	3-as parancsk¢d.				;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
egyhib:
	mov	al,3			; Hibak¢d
	jmp	short hitako		; Hibajelz‚sre
nesimu:
	lds	bx,dword ptr cs:[bufcim]; DS:BX <- pufferc¡m
	mov	word ptr [bx + 12h],0	; Ennyit mozgatott !!!
hitako:
	mov	ah,81h			; Hiba + befejezve
	jmp	short nesive		; Visszat‚r‚sre

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	1-es parancsk¢d. Csere ellen”rz‚s.		;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
csreke:
	lds	bx,dword ptr [bufcim]	; DS:BX <- pufferc¡m
	mov	al,1			; Nincs csere k¢dja
	xchg	cs:[csekod],al		; Ez megy ki ?
	mov	[bx + 14],al		; Ez a csere k¢dja
sivime:
	mov	ah,1			; Sikeres k¢dja
nesive:
	lds	bx,dword ptr cs:[bufcim]; DS:BX <- pufferc¡m
	mov	[bx + 3],ax		; K¢d kipakol s
	pop	bx			; BX vissza
	pop	es			; ES vissza
	pop	ds			; DS vissza
	pop	bp			; BP vissza
	pop	di			; DI vissza
	pop	dx			; DX vissza
	pop	cx			; CX vissza
	pop	ax			; AX vissza
	pop	si			; SI vissza
	retf				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	2-es parancsk¢d. BIOS param‚terblokk k‚r‚s.	;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
bpbker:
	lds	bx,dword ptr [bufcim]	; DS:BX <- pufferc¡m
	mov	word ptr [bx + 12h],offset bpbofs; BPB offszet
	mov	[bx + 14h],cs		; BPB paragrafus
	jmp	sivime			; Sikeres befejez‚sre

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	15-”s parancsk¢d. Csere lek‚rdez‚s.		;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
opcser:
	mov	ax,0300h		; Nem cser‚lhet” k¢dja
	jmp	nesive			; Visszat‚r‚sre

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	4-es parancsk¢d. Szektor(ok) olvas sa.		;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
secrea:
	xor	bh,bh			; Olvas s k¢dja
kozoio:
	call	secbhm			; Szektor I/O
	jnc	sivime			; Sikeres befejez‚sre
	jmp	nesimu			; Nem sikeres

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	8-as parancsk¢d. Szektor(ok) ¡r sa.		;
;	9-es parancsk¢d. Szektor(ok) ¡r sa,		;
;			 majd ™H (ezt el is hagyjuk !)	;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
secwri:
	mov	bh,1			; Ir s k¢dja
	jmp	short kozoio		; A k”z”s I/O-ra

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Szektor I/O.					;
;							;
;	Input:	BH    <- 0 --> olvas s			;
;		BH    <- 1 --> ¡r s			;
;		CX    <- szektorsz m			;
;		DX    <- Kezd” szektor			;
;		ES:DI <- puffer 			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
secbhm:
	cmp	dx,[bpbofs + 8] 	; Szektorsz m
	jc	elfebe			; Ha nem t£l nagy
nikodi:
	mov	al,8			; Hibak¢d
	stc				; Hibajelz” flag
	ret				; Visszat‚r‚s
elfebe:
	mov	ax,dx			; AX <- kezdet
	add	ax,cx			; AX += hossz
	cmp	ax,[bpbofs + 8] 	; Szektorsz m
	ja	nikodi			; Ha t£l nagy
	mov	ax,cx			; Hossz
	mov	cl,byte ptr [bpbofs + 19]; Hatv nysz m
	shl	ax,cl			; AX <- ennyi szektor
	jnc	tulnyu			; Ha belef‚rt AX-be
	mov	ax,8000h		; Ez a maximum most
tulnyu:
	mov	cx,ax			; Uj hossz (sz¢ban)
	mov	ax,dx			; Kezdet
	mul	word ptr [bpbofs]	; Szektor byte-sz m

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	EXTENDED mem¢ria kezel”.			;
;							;
;	Input:	DX:AX <- kezd” poz¡ci¢			;
;		BH    <- 0 --> olvas s			;
;		BH    <- 1 --> ¡r s			;
;		CX    <- sz¢sz m			;
;		ES:DI <- puffer 			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
xmskez:
	mov	si,offset memlei	; Le¡r¢t bla kezd”c¡m
	shl	cx,1			; CX <- byte-sz m
	mov	cs:[si],cx		; A hossz als¢ szava
	mov	word ptr cs:[si + 2],0	; A hossz fels” szava
	or	bh,bh			; Ir s kell ?
	jne	iraxms			; Igen, ¡runk
	mov	bx,[xmshnd]		; XMS handle (c¡m)
	mov	cs:[si + 4],bx		; XMS forr s c¡m
	mov	cs:[si + 6],ax		; XMS H sz¢ 16 bit
	mov	cs:[si + 8],dx		; XMS L sz¢ 16 bit
	mov	word ptr cs:[si + 10],0 ; Forr s (az els” 1 Mbyte)
	mov	cs:[si + 12],di 	; Forr s offszet
	mov	cs:[si + 14],es 	; Forr s paragrafus
	jmp	short csimex
iraxms:
	mov	word ptr cs:[si + 4],0 ; Forr s (az els” 1 Mbyte)
	mov	cs:[si + 6],di		; C‚l offszet
	mov	cs:[si + 8],es		; C‚l paragrafus
	mov	bx,[xmshnd]		; XMS handle
	mov	cs:[si + 10],bx 	; XMS c‚l c¡m
	mov	cs:[si + 12],ax 	; XMS H sz¢ 16 bit
	mov	cs:[si + 14],dx 	; XMS L sz¢ 16 bit
csimex:
	mov	ah,0bh			; XMS move k¢dja
	call	dword ptr [xmsrut]	; XMS kezel” h¡v s
	shr	ax,1			; AL <- v‚grehajt si k¢d
	cmc				; CY <- 0 ha sikeres
	ret				; Visszat‚r‚s

hikehe:
	jmp	short atlhel		; tl‚p‚s
	db	3 dup(90h)		; Ezt v rja az XMS (3 db NOP)
atlhel:
	cmp	ax,0140h		; Ez a hiv¢k¢d ?
	je	nebehi			; Igen, nem bels” h¡v s
eltahi:
	jmp	dword ptr cs:[extruc]	; XMS bels” (jmp short helye)
nebehi:
	push	ax			; AX ment‚s
	push	bx			; BX ment‚s
	push	dx			; DX ment‚s
	mov	ah,8			; XMS m‚retlek‚rdez‚s k¢dja
	call	dword ptr cs:[xmsrut]	; XMS kezel” h¡v s
	mov	ax,dx			; Amit visszaad
	pop	dx			; Ez volt az eredeti
	cmp	ax,dx			; Azonosak ?
	pop	bx			; BX vissza
	pop	ax			; AX vissza
	jne	eltahi			; Ha nem azonosak ism‚tl‚s
	xor	ax,ax			; Ha nem sikerlt
	mov	bl,91h			; XMS zenet szimul l s
	retf				; Visszat‚r‚s

	if	($ - xmskez) AND 1	; Ha a hossz p ratlan
xmskev	equ	$ + 1			; Akkor egyel hosszabb
	else
xmskev	equ	$			; Ha nem, akkor itt a v‚ge
	endif

	dw	376 dup (0)		; Hely a kezel‚sre

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	AX decim lis ki¡rat sa. 			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
decoax:
	cmp	byte ptr cs:[dumtip],0	; Cs”ndben ?
	jnz	nemacs			; Vissza, cs”ndben vagyunk
	mov	cx,10			; Ez az oszt¢
	xor	dx,dx			; H sz¢ <- 0
	div	cx			; Elosztjuk 10-el
	or	ax,ax			; Nincs meg benne ?
	je	makiki			; Nincs, DX <= 9
	push	dx			; A marad‚kot mentem
	call	decoax			; Rekurzi¢ !!!
	pop	dx			; Marad‚k vissza
makiki:
	add	dl,'0'                  ; Karakteres¡t‚s
	mov	ah,2			; Karakterki¡r s k¢dja
	int	21h			; DOS h¡v s
nemacs:
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	0- s parancsk¢d. Eszk”z inicializ l s.		;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
rdinit:
	mov	ds:[0],0ffffh		; Nincs k”vetkez” 1.
	mov	ds:[2],0ffffh		; Nincs k”vetkez” 2.
	mov	[szacit + 2],cs 	; K¢d szegmens ment‚s
	mov	[bpbpar],cs		; K¢d szegmens ment‚s
	cld				; N”vekv” stringmveletek
	mov	ah,30h			; Verzi¢sz m k‚r‚s k¢dja
	int	21h			; DOS h¡v s
	xchg	ah,al			; F” ‚s al verzi¢sz m csere
	mov	[versio],ax		; Elpakoljuk
	cmp	ax,0500h		; Enn‚l kisebb ?
	jb	tulkiv			; Igen, ez baj
	cmp	ah,07h			; Enn‚l nagyobb ?
	jbe	nekero			; Nem, mehet egyb”l
tulkiv:
	mov	dx,offset vershi	; Sz”vegc¡m
	jmp	nesipr			; Kil‚p‚s
nekero:
	lds	si,dword ptr [bufcim]	; DS:SI <- parancssor
	mov	al,[si + 16h]		; Ez az egys‚gsz m
	add	cs:[drvsam],al		; DRIVE sz m
	lds	si,[si + 12h]		; Parancssor pointere
szokat:
	lodsb				; Karakterbeolvas s
	cmp	al,' '                  ; Sz¢k”z ?
	je	szokat			; Igen,  tugorjuk
	cmp	al,9			; Tabul tor ?
	je	szokat			; Igen,  tugorjuk
	cmp	al,','                  ; Vessz” ?
	je	szokat			; Igen,  tugorjuk
	jmp	short maboka		; Ha egyik sem, akkor j¢
pasove:
	mov	dx,offset fejlec	; Ki¡ratand¢ sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	jmp	kesoeu			; Itt kezdjen
kavita:
	jmp	kavike			; Kapcsol¢ vizsg latra
kokald:
	lodsb				; Karakterbeolvas s
maboka:
	cmp	al,0dh			; CR ?
	je	pasove			; Igen, befejezve
	cmp	al,0ah			; LF ?
	je	pasove			; Igen, befejezve
	cmp	al,' '                  ; Sz¢k”z ?
	je	neleza			; Igen, elemezzk
	cmp	al,9			; Tabul tor ?
	je	neleza			; Igen, elemezzk
	cmp	al,','                  ; Vessz” ?
	je	neleza			; Igen, elemezzk
	cmp	al,0			; Stringv‚g ?
	jne	kokald			; Nem, tov bb olvasunk
koloka:
	lodsb				; Karakterbeolvas s
neleza:
	or	al,al			; Nulla ?
	je	pasove			; Igen, befejezve
	cmp	al,0dh			; CR ?
	je	pasove			; Igen, befejezve
	cmp	al,0ah			; LF ?
	je	pasove			; Igen, befejezve
	cmp	al,' '                  ; Sz¢k”z ?
	je	koloka			; Igen, k”vetkez”re
	cmp	al,9			; Tabul tor ?
	je	koloka			; Igen, k”vetkez”re
	cmp	al,','                  ; Vessz” ?
	je	koloka			; Igen, k”vetkez”re
	cmp	al,'/'                  ; Kapcsol¢ jel ?
	je	kavita			; Igen, ki‚rt‚keljk
	cmp	al,'0'                  ; Enn‚l kisebb ?
	jc	cohiva			; Igen, kisebb
	cmp	al,'9'                  ; Nagyobb ?
	ja	cohiva			; Igen, nagyobb
	dec	si			; Vissza a sz mjegyre
	call	conbeo			; Konstans beolvas s
	cmp	cs:[konsam],3		; Harmadik ut n ?
	ja	cohiva			; Igen, hiba
	je	hacobe			; Ha az, mehet
	cmp	cs:[konsam],2		; M sodik ?
	je	szemeo			; Igen, szektorm‚ret
	cmp	bx,4			; Enn‚l nagyobb ?
	jc	cohiva			; Nem, hiba
	cmp	bx,32768		; Nagyobb ?
	jnc	cohiva			; Nagyobb, hiba
	mov	cs:[rammer],bx		; M‚ret
	jmp	short szainc		; Sz ml l¢ n”vel‚sre
szemeo:
	mov	cs:[bpbofs],bx		; Uj m‚ret
	mov	dx,cs:[rammer]		; M‚ret
	mov	al,6			; Ez a kezd” hatv ny
	cmp	bx,128			; Ennyi ? (2**6 sz¢)
	jne	nemmfb			; Nem, k”vetkez”re
	cmp	dx,8192 		; 8 Kbyte-n l kisebb ?
	jc	tamesm			; Igen, ¡gy meg is felel
	shl	bx,1			; Dupl zzuk
nemmfb:
	inc	al			; Hatv ny n”vel‚s
	cmp	bx,256			; Ennyi ? (2**7 sz¢)
	jne	nemmeb			; Nem, k”vetkez”re
	cmp	dx,16384		; 16 Kbyte-n l kisebb ?
	jc	tamesm			; Igen, ¡gy meg is felel
	shl	bx,1			; Dupl zzuk
nemmeb:
	inc	al			; Hatv ny n”vel‚s
	cmp	bx,512			; Ennyi ? (2**8 sz¢)
	jne	pashik			; Ha nem, hiba
tamesm:
	mov	byte ptr cs:[bpbofs + 19],al; Hatv nysz m ment‚s
	cmp	bx,cs:[bpbofs]		; Azonos a tervezettel ?
	je	szainc			; Igen, sz ml l¢ n”vel‚sre
	mov	cs:[bpbofs],bx		; Ez lesz az igazi
	mov	dx,offset secadj	; Sz”vegc¡m
	call	strkir			; Ki¡ratjuk
	jmp	short szainc		; Sz ml l¢ n”vel‚sre
cohiva:
	jmp	short pashik		; Hibaki¡rat s
hacobe:
	cmp	bx,2			; Ennyi van ?
	jc	pashik			; Ha nincs hiba
	cmp	bx,1024 		; Enn‚l nagyobb
	ja	pashik			; Igen, ez hiba
	mov	di,cs:[bpbofs]		; Szektor byte-sz m
	mov	cl,5			; L‚ptet‚ssz m
	shr	di,cl			; Ennyi bejegyz‚s f‚r
	mov	ax,bx			; AX <- konstans
	xor	dx,dx			; DX <- 0
	div	di			; Ennyi bejegyz‚s lehet
	or	dx,dx			; Marad‚k 0 ?
	je	nemasb			; Igen, ennyi bejegyz‚s lesz
	sub	di,dx			; Ennyi maradna
	add	bx,di			; Megn”veljk annyival
nemasb:
	mov	cs:[bpbofs + 6],bx	; Bejegyz‚ssz m
szainc:
	inc	cs:[konsam]		; Konstans sz ml l¢ ++
	jmp	koloka			; Tov bbi param‚terre
pashiq:
	cmp	al,'Q'                  ; Cs”ndben ?
	je	csovab			; Igen, az lesz
	cmp	al,'q'                  ; Cs”ndben ?
	je	csovab			; Igen, az lesz
pashik:
	mov	dx,offset parhib	; Sz”vegc¡m
nesipr:
	call	strkir			; Ki¡rat s DX szerint
kilnue:
	xor	ax,ax			; AX <- egys‚gsz m
	jmp	short kilnie		; Kil‚p‚sre
kavike:
	mov	al,0ffh 		; Ez lesz a jelz”
	xchg	al,cs:[kavoje]		; Kapcsol¢ jelz”
	mov	ah,al			; Ha Quss lenne bel”le
	or	al,al			; Volt m r kapcsol¢ ?
	lodsb				; Kapcsol¢ ‚rt‚k
	jne	pashiq			; Igen, hiba
	cmp	al,'Q'                  ; Cs”ndben ?
	je	csovab			; Igen, az lesz
	cmp	al,'q'                  ; Cs”ndben ?
	jne	expesv			; Nem, esetleg extended
csovab:
	mov	byte ptr cs:[dumtip],0ffh; Nincs duma jelz‚s
	mov	cs:[kavoje],ah		; Kapcsol¢ vissza
	jmp	short koloks		; Mehet elemezni tov bb
expesv:
	cmp	al,'E'                  ; Extended ?
	je	exjeta			; Igen, az lesz
	cmp	al,'e'                  ; Extended ?
	jne	expese			; Nem, esetleg Axpanded
exjeta:
	mov	cs:[drvtip],0		; Extended jelz‚s
koloks:
	jmp	koloka			; Tov bb olvas s
expese:
	cmp	al,'A'                  ; Axpanded ?
	je	expjta			; Igen, az lesz
	cmp	al,'a'                  ; Axpanded ?
	jne	pashik			; Igen, az lesz
expjta:
	mov	cs:[drvtip],1		; Expanded jelz‚s
	jmp	koloks			; Tov bb olvas s
kesoeu:
	push	cs			; Ez kerl majd DS-be
	pop	ds			; DS <- CS
	mov	al,[drvtip]		; T¡pusjelz”
	or	al,al			; Extended ?
	jne	taexin			; Nem, tal n expanded
	call	extini			; Extended kezel” init
	jmp	short inihie		; Hibaellen”rz‚sre
taexin:
	dec	al			; Expanded ?
	jne	morein			; Nem, rendszermem¢ria
	call	expini			; Expanded kezel” init
	jmp	short inihie		; Hibaellen”rz‚sre
morein:
	call	sysini			; Rendszer-mem¢ria kezel”
inihie:
	jnc	mehini			; Ha sikeres volt az init
	jmp	kilnue			; Kil‚p‚s hib val
mehini:
	cmp	byte ptr [inijel],0	; Inicializ lva ?
	jne	noaini			; Nem, akkor inicializ ljuk
	jmp	kilvae			; Sikeres ind¡t sra
noaini:
	call	drvini			; Inicializ ljuk
	jnc	rendin			; Sikeres volt
	call	vimind			; Visszacsin l s
	mov	dx,offset ninele	; Sz”vegc¡m
	jmp	nesipr			; Kil‚p‚s
rendin:
	mov	al,1			; Ennyi egys‚get kezel
kilnie:
	lds	bx,dword ptr cs:[bufcim]; DS:BX <- pufferc¡m
	mov	[bx + 0dh],al		; Egys‚gsz m
	mov	cx,cs:[szacit]		; A m r szabad offszetje
	mov	[bx + 0eh],cx		; Offszet kit”lt‚s
	mov	cx,cs:[szacit + 2]	; A m r szabad paragrafus
	mov	[bx + 10h],cx		; Paragrafus kit”lt‚s
	mov	word ptr [bx + 12h],offset bpbpoi; Ez a BPB pointer
	mov	cx,cs:[bpbpar]		; Ez a BPB paragrafusa
	mov	[bx + 14h],cx		; BPB paragrafus kit”lt‚s
	jmp	sivime			; Sikeres befejez‚sre

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Drive inicializ l s.				;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
drvini:
	mov	ax,[rammer]		; M‚ret (Kbyte)
	mov	cx,1024 		; Ennyi 1 Kbyte
	mul	cx			; DX:AX <- m‚ret
	div	word ptr [bpbofs]	; Szektor byte-sz m
	mov	[bpbofs + 8],ax 	; Szektorsz m
	mov	ax,[bpbofs + 6] 	; Bejegyz‚ssz m
	mov	cl,5			; Egy bejegyz‚s hatv nya
	shl	ax,cl			; AX <- szektor
	xor	dx,dx			; DX <- 0
	div	word ptr [bpbofs]	; Szektor byte-sz m
	or	dx,dx			; Van marad‚k ?
	je	nimase			; Nincs, ennyi szektor
	inc	ax			; Egyel t”bb szektor kell
nimase:
	mov	[rootse],ax		; ROOT hossz
	add	ax,2			; Ennyivel minimum t”bb
	cmp	ax,[bpbofs + 8] 	; Szektorsz m
	jc	szesza			; Ha kevesebb
	mov	word ptr [bpbofs + 6],16; Bejegyz‚ssz m minimum
	xor	dx,dx			; DX <- 0
	mov	ax,512			; AX <- 512
	div	word ptr [bpbofs]	; Szektor byte-sz m
	or	dx,dx			; Van marad‚k ?
	je	manele			; Nincs
	inc	ax			; Ha van, egyel t”bb
manele:
	mov	[rootse],ax		; ROOT hossz
	add	ax,2			; Ennyivel minimum t”bb
	cmp	ax,[bpbofs + 8] 	; Szektorsz m
	jc	szesza			; Ha kevesebb
	stc				; Hibajel
	ret				; Visszat‚r‚s
szesza:
	mov	si,64			; Ciklussz m
itcisa:
	mov	ax,[bpbofs + 8] 	; Szektorsz m
	sub	ax,[bpbofs + 3] 	; Foglaltak sz ma
	mov	cl,byte ptr [bpbofs + 5]; FAT-ok sz ma
	xor	ch,ch			; H byte <- 0
tofale:
	sub	ax,[bpbofs + 11]	; FAT m‚ret
	loop	tofale			; Annyiszor, ah ny FAT van
	sub	ax,[rootse]		; ROOT hossz
	mov	bx,1			; Szektor / cluster
	cmp	ax,0feeh		; 4078-n l t”bb ?
	jc	habeta			; Nem, annyi nincs
	mov	bx,2			; Szektor / cluster
	cmp	ax,1fdch		; 8156-n l t”bb ?
	jc	habeta			; Nem, annyi nincs
	mov	bx,4			; Szektor / cluster
	cmp	ax,3fb8h		; 16312-n‚l t”bb ?
	jc	habeta			; Nem, annyi nincs
	mov	bx,8			; Szektor / cluster
	cmp	ax,7f70h		; 32624-n‚l t”bb ?
	jc	habeta			; Nem, annyi nincs
	mov	bx,16			; Szektor / cluster
habeta:
	xor	dx,dx			; H sz¢ null z s
	div	bx			; AX <- cluster-sz m
	mov	cx,ax			; CX <- cluster-sz m
	shr	cx,1			; CX <- CX / 2
	jnc	parole			; Ha p ros volt
	inc	cx			; Igy is p ros
parole:
	add	ax,cx			; AX <- AX * 1.5
	add	ax,3			; Ennyi kell az elej‚n
	xor	dx,dx			; H sz¢ null z s
	div	word ptr [bpbofs]	; Szektor byte-sz m
	or	dx,dx			; Van marad‚k ?
	je	nimafa			; Ha nincs
	inc	ax			; T”rt FAT szektor
nimafa:
	xchg	ax,[bpbofs + 11]	; FAT m‚ret (az igazi)
	xchg	bl,byte ptr [bpbofs + 2]; Cluster-hossz
	dec	si			; Ciklussz ml l¢ cs”kkent‚s
	je	civeva			; Ha elfogyott az iter l si sz m
	cmp	bl,byte ptr [bpbofs + 2]; Cluster hossz a r‚gi ?
	jne	itcisa			; Nem, vissza sz mol sra
	cmp	ax,[bpbofs + 11]	; FAT m‚ret a r‚gi ?
	jne	itcisa			; Nem, vissza sz mol sra
civeva:
	mov	dx,offset dsizsz	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,[rammer]		; M‚ret (Kbyte)
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset sectsz	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,[bpbofs]		; Szektor byte-sz m
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset alluni	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	al,byte ptr [bpbofs + 2]; Cluster-hossz
	xor	ah,ah			; H byte <- 0
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset entdar	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,[bpbofs + 6] 	; Bejegyz‚ssz m
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset crlfcs	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	cmp	[sysjel],0		; Renszermem¢ria ?
	je	neinre			; Nem, nem a rendszer‚
	mov	ax,1			; BOOT szektor
	add	ax,[bpbofs + 11]	; FAT m‚ret
	add	ax,[rootse]		; ROOT hossz
	mul	word ptr [bpbofs]	; Szektor byte-sz m
	add	ax,[extruc]		; Eltol s L sz¢
	adc	dx,[extruc + 2] 	; Eltol s H sz¢
	add	ax,15			; Paragrafushat ron t£lra tol s
	adc	dx,0			; Carry  tvitel
	mov	cx,16			; Paragrafusm‚ret
	div	cx			; DX:AX <- c¡m
	mov	bx,ax			; BX <- paragrafushossz
	mov	dx,cs			; Ez a k¢dszegmens
	sub	bx,dx			; Ennyi marad a k‚t hely k”z”tt
	cmp	bx,147h 		; Elf‚r a FAT + a ROOT ?
	jnc	elfsys			; Elf‚r, ne b ntsuk
	mov	ax,cs			; Ezt akarom eltolni
	add	ax,147h 		; Ennyivel kerl h tr‚bb
elfsys:
	mov	es,ax			; Ez az £j paragrafus
	xor	si,si			; Innet”l m solajon
	mov	di,si			; Ide tegye
	mov	cx,offset utmasc	; Ilyen hosszan
	cld				; N”vekv” stringmveletek
	rep	movsb			; M sol s
	push	es			; Ez lesz a k¢dszegmens
	mov	ax,offset neinre	; Ide ugorjon
	push	ax			; Offszet Stack-re
	push	es			; Ebb”l lesz DS
	pop	ds			; DS <- £j paragrafus
	retf				; Ugr s az £j k¢dr‚szre
neinre:
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	xor	dx,dx			; Az els”be kell ¡rni
	mov	cx,1			; Ennyit kell ¡rni
	mov	di,offset boots 	; BPB-t kell
	mov	bh,1			; Ir s lesz
	call	mentio			; Szektor ¡r s
	inc	dx			; Az els” FAT szektor
	mov	di,offset secpuf	; Ez a szektor terlete
	xor	ax,ax			; Ezt vigye ki
	mov	cx,512			; Azaz 1024 byte
	cld				; N”vekv” stringmveletek
	rep	stosw			; Kitlt‚s
	mov	di,offset secpuf	; Ez a szektor terlete
	mov	cx,1			; Ennyit kell ¡rni
	mov	es:[di],0fff8h		; Els” k‚t byte
	mov	byte ptr es:[di + 2],0ffh; Harmadik byte
	call	mentio			; Szektor ¡r s
	inc	dx			; A k”vetkez” szektor
	mov	word ptr es:[di],0	; Els” k‚t byte null z s
	mov	byte ptr es:[di + 2],0	; Harmadik byte null z s
	mov	cx,[bpbofs + 11]	; FAT m‚ret
	dec	cx			; Ezt m r fel¡rtuk
	jcxz	egyfat			; Ha csak 1 szektor a FAT
fatcik:
	push	cx			; Ciklussz m ment‚s
	mov	cx,1			; Ennyit kell ¡rni
	call	mentio			; Szektor ¡r s
	inc	dx			; A k”vetkez” szektor
	pop	cx			; Ciklussz m vissza
	loop	fatcik			; Ciklus FAT-ra
egyfat:
	mov	cx,1			; Ennyit kell ¡rni
	mov	di,offset labelh	; Label c¡me
	call	mentio			; Szektor ¡r s
	inc	dx			; A k”vetkez” szektor
	mov	cx,[rootse]		; ROOT hossz
	dec	cx			; Ezt m r fel¡rtuk
	jcxz	kilvae			; Ha csak 1 szektor a ROOT
	mov	di,offset secpuf	; Ez a szektor terlete
rootci:
	push	cx			; Ciklussz m ment‚s
	mov	cx,1			; Ennyit kell ¡rni
	call	mentio			; Szektor ¡r s
	inc	dx			; A k”vetkez” szektor
	pop	cx			; Ciklussz m vissza
	loop	rootci			; Ciklus ROOT-ra
kilvae:
	clc				; Sikeres jelz‚se
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	I/O ment‚sekkel.				;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
mentio:
	push	es			; ES ment‚s
	push	di			; DI ment‚s
	push	ds			; DS ment‚s
	push	cx			; CX ment‚s
	push	dx			; DX ment‚s
	push	bx			; BX ment‚s
	call	secbhm			; Szektor I/O
	pop	bx			; BX vissza
	pop	dx			; DX vissza
	pop	cx			; CX vissza
	pop	ds			; DS vissza
	pop	di			; DI vissza
	pop	es			; ES vissza
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Konstans beolvas¢ BX-be.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
conbeo:
	xor	bx,bx			; Ebben lesz a konstans
conkar:
	lodsb				; Karakter beolvas s
	sub	al,'0'                  ; "Bin ris¡t s"
	jc	beconb			; Ha nem sz mjegy
	cmp	al,9			; Nagyobb mint 9 ?
	ja	beconb			; Igen, nem sz mjegy
	cbw				; AX <- 0000 + AL
	xchg	ax,bx			; AX <- szorzand¢
	mov	dx,10			; Ennyivel kell szorozni
	mul	dx			; AX <- AX * 10
	add	bx,ax			; BX <- BX * 10 + sz mjegy
	jmp	conkar			; K”vetkez” beolvas sra
beconb:
	dec	si			; Visszal‚p‚s
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Visszacsin lunk mindent.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
vimind:
	cmp	[drvtip],1		; EMS a ramdrive ?
	jne	nemexp			; Nem, egy‚b
	mov	dx,[xmshnd]		; EMS handle
	mov	ah,45h			; Close k¢dja
	int	67h			; EMS kezel” h¡v s
tikivi:
	push	ds			; Elrontom mindj rt
	lds	dx,dword ptr [xmsrut]	; DS:DX <- eredeti 19-es vektor
	cli				; IT. most nem j”het
	mov	ax,2519h		; 19-es vektor ¡r s
	int	21h			; DOS h¡v s
	sti				; Most m r h¡vhatja
	pop	ds			; DS vissza
	mov	ax,0ffffh		; Jelz‚s, hogy nincs
	mov	[xmsrut],ax		; Nincs vektor 1.
	mov	[xmsrut + 2],ax 	; Nincs vektor 2.
	ret				; Visszat‚r‚s
nemexp:
	cmp	[sysjel],0		; Rendszermem¢ria ?
	jne	nvahok			; Igen, nem XMS
	mov	dx,[xmshnd]		; XMS handle
	cmp	dx,0ffffh		; Volt kezel” m r ?
	je	nevhnd			; Nem, m‚g nem volt
	mov	ah,0dh			; XMS r”gz¡t‚s elenged‚s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	mov	ah,0ah			; XMS visszaad s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
nevhnd:
	mov	ax,0ffffh		; Jelz‚s, hogy nincs
	cmp	[jmppos],ax		; Volt kezel” m r ?
	jne	tavoke			; Igen, volt m r
	cmp	[jmppos + 2],ax 	; Volt kezel” m r ?
	je	nvahok			; Nem, eddig m‚g nem
tavoke:
	mov	al,0ebh 		; R”vid JMP k¢dja
	mov	ah,[kodofs]		; Ez az offszetje
	push	es			; Elrontom mindj rt
	les	bx,dword ptr [jmppos]	; Ide kell be¡rni
	mov	es:[bx],ax		; Vissza¡rjuk
	mov	ax,9090h		; NOP, NOP k¢dja
	mov	es:[bx + 2],ax		; NOP, NOP vissza¡r s
	mov	byte ptr es:[bx + 4],al ; NOP vissza¡r s
	pop	es			; Visszaveszem
	mov	ax,0ffffh		; Jelz‚s, hogy nincs
	mov	es:[jmppos],ax		; Nem volt kezel” 1.
	mov	es:[jmppos + 2],ax	; Nem volt kezel” 2.
nvahok:
	ret				; Visszat‚r‚s

nuserd:
	xor	dx,dx			; H sz¢ null z s
	mov	ax,dx			; L sz¢ null z s
	mov	cx,512			; Hossz sz¢
	push	cs			; Ez lesz a c‚l paragfusa
	pop	es			; ES <- CS
	mov	di,offset secpuf	; Ez a puffer c¡me
	push	ds			; Elrontja
	call	xmskez			; Byte transzfer
	pop	ds			; DS vissza
	ret				; Visszat‚r‚s
inelle:
	xor	bh,bh			; BH <- olvas s
	call	nuserd			; Beolvastunk
	mov	dx,offset iohiba	; Sz”vegc¡m
	jc	hibles			; Kil‚p‚s hib val
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	mov	di,offset secpuf	; Puffer
	mov	si,offset boots 	; BPB c¡m
	mov	cx,11			; Ennyit kell ”sszehasonl¡tani
	cld				; N”vekv” stringmveletek
	rep	cmpsb			; ™sszehasonl¡t s
	jne	nemini			; Ha nem azonos, vissza
	cmp	byte ptr [inijel],2	; Inicializ l s ut n ?
	je	nemini			; Igen, vissza
	mov	si,offset secpuf + 11	; Eltoljuk
	lodsw				; AX <- hossz
	cmp	ax,[bpbofs]		; Szektor byte-sz m
	jne	nemini			; Ha nem azonos, vissza
	mov	si,offset secpuf + 17	; Eltoljuk
	lodsw				; Bejegyz‚sek sz ma
	cmp	ax,[bpbofs + 6] 	; Bejegyz‚ssz m
	jne	nemini			; Ha nem azonos, vissza
	mov	byte ptr [inijel],0	; Inicializ lva
	mov	di,offset bpbofs	; Szektor byte-sz m c¡me
	mov	si,offset secpuf + 11	; M sik byte-sz m c¡me
	mov	cx,10			; Hossz szavakban
	rep	movsw			; tm soljuk
nemini:
	clc				; St tuszbe ll¡t s
	ret				; Visszat‚r‚s
hibles:
	jmp	strkir			; Ki¡rat s DX szerint

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Extended kezel” inicializ l s.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
extini:
	call	xmsope			; XMS handle nyit s
	jc	handni			; Nem sikerlt
extrui:
	mov	bx,[xmsrut]		; XMS kezel” offszet
	mov	es,[xmsrut + 2] 	; XMS kezel” paragrafus
vijmpv:
	mov	[extruc + 2],es 	; XMS bels” paragrafus
	mov	[jmppos + 2],es 	; Poz¡ci¢ paragrafus
	mov	[jmppos],bx		; Poz¡ci¢ offszet
	mov	cx,es:[bx]		; Ez a k¢d van ott
	cmp	cl,0ebh 		; JMP r”vid k¢dja ?
	je	rojako			; Igen, r”vid JMP k¢d
	cmp	cl,0eah 		; JMP SEG:OFFSET k¢dja ?
	jne	badkez			; Nem, hiba van
	push	es:[bx + 1]		; Offszet beolvas s
	mov	es,es:[bx + 3]		; Paragrafusc¡m beolvas s
	pop	bx			; BX <- offszet
	jmp	vijmpv			; N‚zzk meg ott is
rojako:
	cmp	es:[bx + 2],9090h	; NOP, NOP van ott ?
	jne	badkez			; Nem, hiba van
	cmp	byte ptr es:[bx + 4],90h; NOP van ott ?
	jne	badkez			; Nem, hiba van
	mov	di,bx			; DI <- offszet
	mov	ah,0			; AH <- 0
	mov	al,ch			; AL <- k¢d offszet
	mov	[kodofs],al		; Eltesszk
	add	ax,2			; Eltoljuk a r”vid JMP hossz val
	add	bx,ax			; BX <- ez az £j offszet
	mov	[extruc],bx		; XMS bels” offszet
	mov	byte ptr es:[di],0eah	; JMP SEG:OFFSET k¢dja
	mov	es:[di + 1],offset hikehe; Offszet
	push	[bpbpar]		; Ezzel t”ltse ki
	pop	es:[di + 3]		; Paragrafus
	clc				; Sikeres volt
handni:
	ret				; Visszat‚r‚s
badkez:
	mov	dx,offset badext	; Sz”vegc¡m
	jmp	strkir			; Ki¡rat s DX szerint

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Expanded kezel” inicializ l s.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
expini:
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	mov	si,offset emskez	; Saj t rutin c¡me
	mov	di,offset xmskez	; Kezel” fejc¡m
	mov	cx,(emskev - emskez) / 2; A rutin helym‚rete sz¢ban
	cli				; IT. ne j”jj”n
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	call	emsope			; EMS nyit s
	jc	rovite			; Hib s a nyit s
	mov	ax,3519h		; 19-es vektor lek‚rdez‚s
	int	21h			; DOS h¡v s
	mov	[xmsrut],bx		; Offszet elpakol s
	mov	[xmsrut + 2],es 	; Paragrafus elpakol s
	mov	dx,offset emsk19 - (emskez - xmskez); Ez a kezel” c¡me
	mov	ax,2519h		; 19-es vektor m¢dos¡t s
	int	21h			; DOS h¡v s
	call	inelle			; Megn‚zzk volt-e inicializ lva
	jnc	siinel			; Ha sikeres volt
	call	vimind			; Visszacsin l s
	stc				; Hibajelz‚s
rovite:
	ret				; Visszat‚r‚s
siinel:
	cmp	byte ptr [inijel],0	; Inicializ lva ?
	jne	nermet			; Nem, meg van az ‚rt‚k
	mov	ax,[bpbofs + 8] 	; Szektorsz m
	mul	word ptr [bpbofs]	; Szektor byte-sz m
	mov	cx,1024 		; Ez 2**10
	div	cx			; AX <- Kbyte
	cmp	ax,[rammer]		; M‚ret (Kbyte)
	je	nermet			; Ha azonos
	mov	byte ptr [inijel],2	; Nem azonos jelz‚se
nermet:
	clc				; Sikeres jelz‚se
	ret				; Visszat‚r‚s

emmxxx	db	'EMMXXXX0'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;						;
;	EXPANDED mem¢ria kezel”.		;
;						;
;	Input:	DX:AX <- kezd” poz¡ci¢		;
;		BH    <- 0 --> olvas s		;
;		BH    <- 1 --> ¡r s		;
;		CX    <- sz¢sz m		;
;		ES:DI <- puffer 		;
;						;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
emskez:
	push	ax			; Darabsz m L sz¢
	push	dx			; Darabsz m H sz¢
vasama:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,47h			; Save k¢dja
	int	67h			; EMS kezel” h¡v s
	or	ah,ah			; Sikeres volt ?
	je	sisaco			; Igen, sikere
	cmp	ah,82h			; EMS foglalt ?
	je	vasama			; Igen, v runk
	pop	dx			; Darabsz m H sz¢ r¡t‚s
	pop	dx			; Darabsz m L sz¢ r¡t‚s
	mov	al,2			; Hibak¢d
	cmp	ah,8ch			; T£lcsordul s ?
	je	messem			; Igen, nem megy a save
	cmp	ah,8dh			; Enn‚l nagyobb ?
	ja	messem			; Nem, mehet
	mov	al,0ch			; Hibak¢d
messem:
	stc				; Hibajelz‚s
	ret				; Visszat‚r‚s
sisaco:
	pop	dx			; Darabsz m H sz¢
	pop	ax			; Darabsz m L sz¢
nekevi:
	push	cx			; Sz¢sz m
	mov	cx,16384		; Ez egy lap hossza
	div	cx			; AX <- lapsz m, DX <- marad‚k
	mov	si,dx			; SI <- marad‚k
	mov	dx,ax			; DX <- lapsz m
	pop	cx			; Ennyi sz¢ kell
	push	bx			; Ez az ¡r s olvas s jelz”
	push	cx			; Sz¢ darab
	mov	ax,di			; Output c¡m
	add	ax,1			; Lehet esetleg carry
	rcr	ax,1			; Igy legfeljebb 8000h
	dec	cx			; Sz¢ darab - 1
	add	ax,cx			; C¡m maximum
	shr	ax,1			; Lel‚ptet‚s 1.
	shr	ax,1			; Lel‚ptet‚s 2.
	shr	ax,1			; Lel‚ptet‚s 3.
	mov	bx,es			; C‚l szegmens
	add	ax,bx			; AX <- paragrafus-tet”
	sub	ax,[extruc + 2] 	; Nagyobb a page-frame ?
	jc	egysol			; Ha nagyobb volt
	mov	cx,4			; Hatv ny az oszt shoz
	mov	bp,di			; BP <- startc¡m
	shr	bp,cl			; BP <- paragrafus
	add	bp,bx			; BP <- paragrafustet”
	sub	bp,[extruc + 2] 	; Igy el‚rte a page-frame-t ?
	jc	eleati			; Ha el‚rte
	cmp	bp,4096 		; Nagyobb mint 64K ?
	jnc	egysol			; Igen, mehet
eleati:
	jmp	leonre			; Visszair s EMS-re
egysol:
	pop	cx			; Sz¢sz m
	pop	bx			; Ir s/Olvas s jelz”
	xor	bp,bp			; BP <- 0
	push	bx			; Jelz” vissza
	mov	ax,dx			; AX <- lapsz m
	mov	bx,si			; BX <- marad‚k lapm‚ret
	shr	bx,1			; BX <- marad‚k sz¢ban
	push	cx			; Sz¢sz m ment‚s
	add	bx,cx			; Ez az utols¢ c¡m (szavas !)
	mov	dx,bx			; M solat a c¡mr”l
	mov	cl,13			; A hatv nysz m lapm‚retre
	shr	bx,cl			; BX <- lapsz m
	and	dx,1fffh		; Ennyi sz¢ marad le a lapr¢l
	je	nemase			; Nem, eg‚sz lapra esik
	inc	bx			; Egy lappal t”bb kell
nemase:
	mov	cx,bx			; Ennyi lap kellet
	mov	bx,ax			; BX <- kezd” lapsz m
	cmp	cx,4			; Nem t”bb mint 4 ?
	jbe	nenmax			; Nem, mehet
	mov	bp,dx			; Ennyi maradt m solni
	dec	cx			; Mert legfeljebb 5 volt
	pop	ax			; Sz¢sz m vissza
	sub	ax,dx			; Marad‚kkal cs”kkentve
	push	ax			; Ez az £j sz¢sz m
nenmax:
	mov	dx,[xmshnd]		; EMS handle
	mov	ax,4400h		; EMS map k¢dja
	push	ax			; Eltesszk stackre
valekma:
	pop	ax			; K¢d el”v‚tel (AX t”lt‚s)
	push	ax			; Visszatesszk
	push	bx			; Lapsz m ment‚s
	push	dx			; EMS handle ment‚s
	int	67h			; EMS kezel” h¡v s
	pop	dx			; EMS handle vissza
	pop	bx			; Lapsz m vissza
	or	ah,ah			; Sikeres ?
	jne	nesilem 		; Nem, m s k¢d j”tt
	inc	bx			; K”vetkez” lapsz m
	pop	ax			; EMS map k¢d
	inc	al			; K”vetkez” lap
	push	ax			; K¢d stackre
	loop	valekma 		; Lek‚pz‚si ciklus
	pop	ax			; Stack r¡t‚s (MAP k¢d)
	pop	cx			; Ennyi sz¢ megy ebben a menetben
	pop	ax			; Ez az ¡r s/olvas s jelz”
	push	ax			; Vissza is tesszk
	or	ah,ah			; Olvas s ?
	je	emsolv			; Igen olvasunk
	push	es			; ES b”l lesz DS
	push	di			; Ez megy majd SI-be
	mov	di,si			; DI <- c‚l
	pop	si			; SI <- r‚gi DI
	pop	ds			; DS <- ES
	mov	es,cs:[extruc + 2]	; Page-frame paragrafus a c‚l
	jmp	short masveg		; Mehet a mozgat s
emsolv:
	mov	ds,[extruc + 2] 	; Page-frame paragrafus a forr s
masveg:
	rep	movsw			; tm soljuk
	or	bp,bp			; Maradt ?
	jne	marlap			; Igen,  tl‚pjk
siemsm:
	pop	ax			; Ez kell r¡t‚s miatt
	clc				; St tusz kit”lt‚s
resems:
	push	ax			; Kil‚p‚si k¢d
	pushf				; St tusz kipakol s
varres:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,48h			; Restore k¢dja
	int	67h			; EMS kezel” h¡v s
	or	ah,ah			; Sikeres ?
	je	valner			; Igen, sikeres
	cmp	ah,82h			; EMS foglalt ?
	je	varres			; Igen, v runk
	cmp	ah,8eh			; Restore hiba ?
	je	valner			; Igen, az
	pop	dx			; Stack r¡t‚s
	pop	dx			; Stack r¡t‚s
	mov	al,0ch			; Hibak¢d
	stc				; Hibajel
	ret				; Visszat‚r‚s
valner:
	popf				; Flag <- sikeres
	pop	ax			; Stack r¡t‚s
	ret				; Visszat‚r‚s
marlap:
	mov	dx,cs:[xmshnd]		; EMS handle
marlal:
	mov	ax,4400h		; EMS map k¢dja
	push	bx			; Lapsz m ment‚s
	push	dx			; EMS handle ment‚s
	int	67h			; EMS kezel” h¡v s
	pop	dx			; EMS handle vissza
	pop	bx			; Lapsz m vissza
	or	ah,ah			; Sikeres ?
	jne	nesmal			; Nem, valami baj van
	pop	ax			; Ez az ¡r s/olvas s jelz”
	push	ax			; Vissza is tesszk
	or	ah,ah			; Olvas s ?
	je	olvmara 		; Igan, marad‚k olvas s
	xor	di,di			; Itt a c‚l c¡me
	jmp	short mairmo		; Mehet a marad‚k m sol sa
olvmara:
	xor	si,si			; Itt a forr s c¡me
mairmo:
	mov	cx,bp			; A marad‚k hossza sz¢ban
	rep	movsw			; tm soljuk
	jmp	siemsm			; tmegynk a befejez‚shez
nesilem:
	cmp	ah,82h			; EMS foglalt ?
	je	valekma 		; Igen, v runk
	pop	dx			; 1 sz¢ r¡t‚s
	pop	dx			; 1 sz¢ r¡t‚s
	jmp	short nejoli		; Hibak¢ddal kil‚p‚s
nesmal:
	cmp	ah,82h			; EMS foglalt ?
	je	marlal			; Igen, v runk
nejoli:
	pop	dx			; 1 sz¢ r¡t‚s
	mov	al,2			; Hiba k¢d
	stc				; Hibajelz”
	jmp	resems			; Kil‚p‚s restore-ral
leonre:
	xor	al,al			; Null s lap
	mov	bx,[extruc + 2] 	; Ez a page-frame
	cmp	bp,4096 		; El”tte kezd”dik ?
	jnc	naminn			; Igen, el”tte
	cmp	bp,1024 		; Nagyobb mint 16K ?
	jnc	namine			; Igen, nagyobb
naminn:
	mov	al,3			; Negyedik 16K-s lap
	add	bx,0c00h		; Page-frame is eltolva
namine:
	add	bp,0800h		; Eltoljuk k‚t lappal
	mov	cx,bp			; CX <- eltolt m solat
	mov	ds,bx			; Kisz m¡tott paragrafus
	pop	bp			; Sz¢ darab
	pop	bx			; Ez az ¡r s olvas s jelz”
	push	cx			; Eltolt ment‚se
	xor	cx,cx			; Marad‚k, ha kisebb lenne
	cmp	bp,16384		; Nagyobb mint a lapm‚ret ?
	jna	lamene			; Nem nagyobb, akkor csak enyyi
	mov	cx,bp			; Lem soljuk a m‚retet
	mov	bp,16384		; Ez a lapm‚ret
	sub	cx,bp			; CX <- marad‚k
lamene:
	push	cx			; Marad‚k ment‚s
	push	bx			; Ez az ¡r s olvas s jelz”
	push	dx			; Lapsz m
	mov	cx,16384		; Ez a lapm‚ret
	sub	cx,si			; Kezd”c¡met levonjuk
	shr	cx,1			; CX <- sz¢sz m
	cmp	cx,bp			; Kell ennyi egy ltal n ?
	jc	kelmem			; Igen, nem koorig lunk
	mov	cx,bp			; Csak ennyit kell
kelmem:
	or	bh,bh			; Olvas s ?
	je	csaolo			; Igen, nincs csere
	push	ds			; Ez lesz k‚s”bb ES
	push	es			; Ez lesz k‚s”bb DS
	push	si			; Ez lesz k‚s”bb DI
	mov	si,di			; SI <- DI
	pop	di			; Ez a r‚gi SI
	pop	ds			; Ez a r‚gi ES
	pop	es			; Ez a r‚gi DS
csaolo:
	cld				; N”vekv” stringmveletek
elacse:
	sub	bp,cx			; BP <- ennyit olvastunk
	mov	bx,dx			; Lapsz m m solat
	mov	ah,44h			; MAP k¢dja
	mov	dx,cs:[xmshnd]		; EMS handle
	push	ax			; K¢d ment‚s (AL miatt)
varonm:
	pop	ax			; AX <- mveleti k¢d
	push	ax			; Vissza is tesszk
	push	bx			; Lapsz m ment‚s
	push	dx			; Handle ment‚s
	int	67h			; EMS kezel” h¡v s
	pop	dx			; Handle vissza
	pop	bx			; Lapsz m vissza
	or	ah,ah			; Sikeres volt ?
	je	sikonm			; Igen, sikeres
	cmp	ah,82h			; EMS foglalt ?
	je	varonm			; Igen, v runk r 
	pop	ax			; Stack r¡t‚s
	mov	al,2			; Hibak¢d
	pop	dx			; 1 sz¢ r¡t‚s
	pop	dx			; 1 sz¢ r¡t‚s
	pop	dx			; 1 sz¢ r¡t‚s
	stc				; Hibajelz‚s
	jmp	hiklva			; Kil‚p‚sre (CARRY set !!!)
sikonm:
	pop	ax			; Lapk¢d vissza
	rep	movsw			; M solunk
	or	bp,bp			; Maradt ?
	je	nemonm			; Nem maradt
	pop	dx			; Lapsz m
	pop	bx			; Ir s olvas s jelz”
	inc	dx			; K”vetkez” lap
	push	bx			; Jelz” vissza
	push	dx			; Lapsz m vissza
	mov	cx,2000h		; Ez egy lap hossza sz¢ban
	cmp	cx,bp			; T”bb mint egy lap ?
	jna	tomine			; Igen, t”bb mint egy lap
	mov	cx,bp			; Ha nem t”bb akkor annyi
tomine:
	or	bh,bh			; Olvas s ?
	jne	irakit			; Nem, ¡r s
	xor	si,si			; Forr s c¡m
	jmp	elacse			; Vissza a k”vetkez” lapra
irakit:
	xor	di,di			; C‚l c¡m
	jmp	elacse			; Vissza a k”vetkez” lapra
nemonm:
	pop	dx			; Lapsz m
	pop	bx			; Ir s olvas s jelz”
	pop	bp			; M sodik menetre marad‚k
	pop	ax			; A mentett marad‚k
	or	bp,bp			; Maradt ?
	jne	marmam			; Igen, maradt
	jmp	onreve			; Nem maradt, v‚ge
marmam:
	push	ax			; Marad‚k ment‚s
	xor	cx,cx			; CX <- 0
	push	cx			; Stack-re 0
	clc				; Sikeres jelz‚se
	push	dx			; Lapsz m ment‚s
	push	ax			; Marad‚k ment‚s
	pushf				; Flag ment‚s
resonv:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,48h			; Restore k¢dja
	int	67h			; EMS kezel” h¡v s
	or	ah,ah			; Sikeres volt ?
	je	sikmro			; Igen, sikeres
	cmp	ah,82h			; EMS foglalt ?
	je	resonv			; Igen, v runk
	cmp	ah,8eh			; M r volt restore ?
	je	sikmro			; Igen, ¡gy is sikeres
	pop	dx			; Stack r¡t‚s
	pop	dx			; Stack r¡t‚s
	mov	al,0ch			; Hibak¢d
	stc				; Hib s jelz‚se
	jmp	short hiklev		; Kil‚p‚sre
sikmro:
	popf				; Sikereresre  ll¡tva a flag
	pop	ax			; Marad‚k vissza
hiklev:
	pop	dx			; Lapsz m vissza
	jnc	resoke			; Ha sikeres
	pop	dx			; Stack r¡t‚s
	pop	dx			; Stack r¡t‚s
	ret				; Visszat‚r‚s
resoke:
	push	ax			; Marad‚k ment‚s
	push	dx			; Lapsz m ment‚s
varosa:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,47h			; Save mapping k¢dja
	int	67h			; EMS kezel” h¡v s
	or	ah,ah			; Sikeres volt ?
	je	sikvom			; Igen, sikeres volt
	cmp	ah,82h			; EMS foglalt ?
	je	varosa			; Igen, v runk
	pop	dx			; Lapsz m elimin l s
	pop	dx			; Marad‚k vissza
	mov	al,2			; Visszat‚r‚si hibak¢d
	cmp	ah,8ch			; Full ?
	je	hisava			; Igen, hiba
	cmp	ah,8dh			; Volt m r ?
	ja	hisava			; Igen, hiba
	mov	al,0ch			; Ez a m sik hibak¢d
hisava:
	stc				; Hibajel
	pop	dx			; Stack r¡t‚s
	pop	dx			; Stack r¡t‚s
	ret				; Visszat‚r‚s
sikvom:
	pop	dx			; Lapsz m vissza
	pop	ax			; Marad‚k vissza
	mov	cx,cs:[extruc + 2]	; Page-frame
	cmp	ax,0400h		; Enn‚l nagyobb ?
	jc	nkinan			; Nem, harmadik lapra
	cmp	ax,1000h		; Enn‚l kisebb ?
	jnc	nkinan			; Nem, harmadik lapra
	xor	ax,ax			; Lapk¢d 0
	jmp	short eltola		; Az els” lap kellet
nkinan:
	mov	al,3			; Lapk¢d 3
	add	cx,0c00h		; Page-frame is eltolva
eltola:
	or	bh,bh			; Olvas s ?
	jne	moirmm			; Nem ¡r s
	mov	ds,cx			; Ez a forr s
	mov	cx,16384		; Ez egy lap
	cmp	si,cx			; Nagyobb a lapm‚retn‚l ?
	jna	lamerk			; Nem, ezen a lapon van
	inc	dx			; K”vetkez” lap
	xor	si,si			; Ott null n kezd
lamerk:
	sub	cx,si			; CX <- mozgatand¢ byte-sz m
	shr	cx,1			; CX <- mozgatand¢ sz¢-sz m
darkes:
	push	bx			; Ir s-olvas s jelz” ment‚s
	push	dx			; Lapsz m ment‚s
	cmp	cx,bp			; Kell-e m‚g annyit ?
	jna	bikemo			; Igen, kell
	mov	cx,bp			; Nem, csak ennyit
bikemo:
	jmp	elacse			; Vissza a k”vetkez” lapra
moirmm:
	mov	es,cx			; Ez a c‚l
	mov	cx,16384		; Ez egy lap
	cmp	di,cx			; Nagyobb a lapm‚retn‚l ?
	jc	lamerw			; Nem, ezen a lapon van
	xor	di,di			; Ott null n kezd
	inc	dx			; K”vetkez” lap
lamerw:
	sub	cx,di			; CX <- mozgatand¢ byte-sz m
	shr	cx,1			; CX <- mozgatand¢ sz¢-sz m
	jmp	darkes			; Mehet a marad‚kra
onreve:
	clc				; Sikeres jelz‚se
hiklva:
	push	ax			; K¢d ment‚s
	pushf				; Sikeres jelz” ment‚se
urfova:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,48h			; Restore k¢dja
	int	67h			; EMS kezel” h¡v s
	or	ah,ah			; Sikeres volt ?
	je	sikutr			; Igen, sikeres
	cmp	ah,82h			; EMS foglalt ?
	je	urfova			; Igen, v runk
	cmp	ah,8eh			; M r volt k¢dja ?
	je	sikutr			; Igen, akkor is sikeres
	pop	dx			; Stack r¡t‚s
	pop	dx			; Stack r¡t‚s
	mov	al,0ch			; Hibak¢d
	stc				; Hibajelz‚s
	ret				; Visszat‚r‚s
sikutr:
	popf				; Sikeres jelz” vissza
	pop	ax			; K¢d vissza
	ret				; Visszat‚r‚s
emsk19:
	push	ax			; AX ment‚s
	push	dx			; DX ment‚s
varclr:
	mov	dx,cs:[xmshnd]		; EMS handle
	mov	ah,45h			; EMS visszaad s
	int	67h			; EMS kezel” h¡v s
	cmp	ah,82h			; EMS foglalt ?
	je	varclr			; Igen, v runk
	pop	dx			; DX vissza
	push	ds			; DS elromlik
	xor	ax,ax			; AX <- 0
	mov	ds,ax			; DS <- 0
	mov	ax,cs:[xmsrut]		; 19-es offszet
	cli				; Most IT. ne j”jj”n
	mov	ds:[19h * 4],ax 	; Offszet vissza
	mov	ax,cs:[xmsrut + 2]	; 19-es paragrafusa
	mov	ds:[(19h * 4) + 2],ax	; Paragrafus vissza
	pop	ds			; DS vissza
	pop	ax			; AX vissza
	int	19h			; Folytassa

	if	($ - emskez) AND 1	; Ha a hossz p ratlan
emskev	equ	$ + 1			; Akkor egyel hosszabb
	else
emskev	equ	$			; Ha nem, akkor itt a v‚ge
	endif

rammer	dw	40h			; M‚ret (Kbyte-ban)
konsam	db	1			; Konstans sz ml l¢
inijel	db	1			; Inicializ l s jelz”
kavoje	db	0			; Kapcsol¢ volt jelz”
rootse	dw	0			; ROOT hossz

	if	xmskev GT emskev - (emskez - xmskez)
szacit	dw	xmskev,0		; A szabad ha XMS nagyobb
	else
szacit	dw	emskev - (emskez - xmskez),0; A szabad ha EMS nagyobb
	endif

bpbpar	dw	0			; A BPB paragrafusa
versio	dw	0			; Ide j”n majd a verzi¢
sysjel	db	0			; Rendszermem¢ria jelz”

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	XMS ind¡t¢.					;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
xmsope:
	mov	ax,4300h		; Ez a k‚rdez‚s k¢dja
	int	2fh			; MULTIPLEX
	cmp	al,80h			; Sikeres ?
	je	talxms			; Igen, van XMS
	mov	dx,offset xmsnot	; Sz”vegc¡m
nemsim:
	jmp	strkir			; Ki¡rat s DX szerint
talxms:
	push	es			; ES romlik
	mov	ax,4310h		; Vektor k‚r‚s k¢dja
	int	2fh			; MULTIPLEX
	mov	[xmsrut],bx		; XMS kezel” offszet
	mov	[xmsrut + 2],es 	; XMS kezel” paragrafus
	pop	es			; ES vissza
	mov	ah,8			; XMS m‚retlek‚rdez‚s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	or	ax,ax			; Volt hiba ?
	jne	hitani			; Nem, tal n nem
	test	bl,80h			; Van extended mem¢ria ?
	mov	dx,offset noexme	; Sz”vegc¡m
	je	nemsim			; Nincs, az‚rt van a hiba
	mov	dx,offset exmalh	; Sz”vegc¡m
	jmp	short nemsim		; Hib val kil‚p‚s
hitani:
	mov	dx,offset ninele	; Sz”vegc¡m
	cmp	ax,10h			; Enn‚l kisebb a k¢d ?
	jc	nemsim			; Igen, nincs mem¢ria
	cmp	[rammer],ax		; Nagyobb mint a m‚ret ?
	jna	nemnab			; Ha nem nagyobb mint a m‚ret
	mov	[rammer],ax		; Uj m‚ret (Kbyte)
nemnab:
	mov	dx,[rammer]		; M‚ret (Kbyte)
	mov	ah,9			; XMS allok l s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	or	ax,ax			; Volt hiba ?
	jne	vanrac			; Nem, sikerlt
	mov	dx,offset exmalh	; Sz”vegc¡m
	jmp	short nemsim		; Hib val kil‚p‚s
vanrac:
	mov	[xmshnd],dx		; XMS handle
	ret				; Visszat‚r‚s (CY <- 0 !!!)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	EMS ind¡t¢.					;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
emsope:
	mov	ax,3567h		; 67-es vektor lek‚rdez‚s
	int	21h			; DOS h¡v s
	mov	di,0ah			; Eszk”zn‚v offszet
	mov	si,offset emmxxx	; Itt van az EMS neve
	mov	cx,8			; Ennyi a n‚v
	rep	cmpsb			; ™sszehasonlit s
	je	talkez			; Ha simmel, van EMS
	mov	dx,offset emsnin	; Sz”vegc¡m
	jmp	strkir			; Ki¡rat s DX szerint
talkez:
	mov	cx,8000h		; Ennyiszer v runk
streva:
	mov	ah,40h			; St tusz beolvas s k¢dja
	int	67h			; EMS kezel” h¡v s
	cmp	ah,0			; Sikeres ?
	je	sikstk			; Igen, mehet
	cmp	ah,82h			; EMS foglalt ?
	loopz	streva			; Igen, v runk
emskhi:
	mov	dx,offset emssta	; Sz”vegc¡m
emshib:
	jmp	strkir			; Kil‚pnk, hiba van
sikstk:
	mov	ah,46h			; Mi a paragrafusa ?
	int	67h			; EMS kezel” h¡v s
	cmp	ah,82h			; EMS foglalt ?
	je	sikstk			; Igen, v runk
	cmp	ah,0			; Sikeres ?
	jne	emskhi			; Nem, hibazenet
	cmp	al,40h			; A kezel” van 4.00 ?
	jc	nemjve			; Nincs, nem n‚zzk meg
	mov	ax,5800h		; Lek‚pz‚sk‚r‚s k¢dja
	push	ds			; Ebb”l lesz ES
	pop	es			; ES <- DS
	mov	di,offset secpuf	; Itt van szabad terlet
	mov	si,di			; Innen kell beolvasni is
	int	67h			; EMS kezel” h¡v s
	cmp	ah,0			; Sikeres ?
	jne	emskhi			; Nem, hibazenet
	cld				; N”vekv” stringmveletek
ofnuke:
	lodsw				; Paragrafus
	lodsw				; Ofszet
	or	ax,ax			; Lapsz m = 0 ?
	je	ofnuta			; Igen, az offszet nulla
	loop	ofnuke			; Ha van m‚g, akkor tov bb
ofnuta:
	mov	dx,ax			; Offszet m solat
	mov	bx,[si - 4]		; Ez a paragrafusc¡m
	mov	cx,3			; M‚g 3 lapot ellen”rz”k
ezalap:
	inc	dx			; Lapsz m n”vel‚s
	add	bx,1024 		; Ennyi paragrafus egy lap
	lodsw				; K”vetkez” paragrafusc¡m
	cmp	ax,bx			; Ez az a c¡m ?
	jne	emskhi			; Nem, hibazenet
	lodsw				; Lapsz m beolvas s
	cmp	ax,dx			; Ez az a lapsz m ?
	jne	emskhi			; Nem, hibazenet
	loop	ezalap			; Lapellen”rz” ciklus
nemjve:
	mov	ah,41h			; Mi a paragrafusa ?
	int	67h			; EMS kezel” h¡v s
	cmp	ah,82h			; EMS foglalt ?
	je	nemjve			; Igen, v runk
	cmp	ah,0			; Sikeres ?
	jne	emskhi			; Nem, hibazenet
	mov	[extruc],0		; Offszet
	mov	[extruc + 2],bx 	; Paragrafus
vaheke:
	mov	ah,42h			; Helyk‚rd‚s
	int	67h			; EMS kezel” h¡v s
	cmp	ah,82h			; EMS foglalt ?
	je	vaheke			; Igen, v runk
	cmp	ah,0			; Sikeres ?
	jne	emskhi			; Nem, hibazenet
	mov	ax,dx			; Ez a teljes hossz
	mov	dx,offset noexme	; Sz”vegc¡m
	or	ax,ax			; Van valamennyi ?
	jne	ehiban			; Igen, van
emshit:
	jmp	emshib			; Nincs ki¡rjuk a hib t
ehiban:
	mov	dx,offset ninele	; Sz”vegc¡m
	or	bx,bx			; Van szabad ?
	je	emshit			; Nincs, ki¡rjuk a hib t
	test	bx,0f000h		; A fels” 4 bit van ?
	jne	nemeva			; Igen, ennyi van
	mov	cx,4			; Hatv nyoz¢ ‚rt‚k
	shl	bx,cl			; BX <- Kbyte
	cmp	[rammer],bx		; A m‚ret belef‚r ?
	jna	nemeva			; Igen, belef‚r
	mov	[rammer],bx		; Uj m‚ret (Kbyte)
nemeva:
	mov	bx,[rammer]		; M‚ret (Kbyte)
	mov	ax,bx			; M‚ret m solat
	mov	cx,4			; Hatv nyoz¢ ‚rt‚k
	shr	bx,cl			; BX <- lapsz m
	test	ax,0fh			; Volt marad‚k ?
	je	poanyi			; Nem, ennyivel ennyi
	inc	bx			; Egy lappal t”bb
	push	bx			; Ez az £j m‚ret
	mov	cx,4			; Hatv nyoz¢ ‚rt‚k
	shl	bx,cl			; BX <- m‚ret (Kbyte)
	mov	[rammer],bx		; M‚ret (Kbyte)
	pop	bx			; Lapsz m vissza
poanyi:
	mov	ah,43h			; Open handle k¢dja
	int	67h			; EMS kezel” h¡v s
	cmp	ah,82h			; EMS foglalt ?
	je	poanyi			; Igen, v runk
	cmp	ah,0			; Sikeres ?
	je	sifopa			; Igen, sikeres
	cmp	ah,86h			; Save hiba ?
	je	fahita			; Igen, fat lis
	cmp	ah,85h			; Nincs t”bb handle ?
	jc	fahita			; Ha enn‚l kisebb a hiba
	mov	dx,offset ninele	; Sz”vegc¡m
	jmp	strkir			; Ki¡rat s, hiba van
fahita:
	jmp	emskhi			; Fat lis hib ra
sifopa:
	mov	[xmshnd],dx		; EMS handle
	mov	si,offset kprdve	; ™ a tulajdonos
	mov	ax,5301h		; Tulajdonos be ll¡t s k¢dja
	int	67h			; Tulajdonos k‚rd‚s
	cmp	ah,0			; Sikeres ?
	jne	fahita			; Nem, hibazenet
	clc				; Sikeres jelz‚se
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Rendszermem¢ria inicializ l¢.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sysini:
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	mov	si,offset syskez	; Saj t rutin c¡me
	mov	di,offset xmskez	; Kezel” fejc¡m
	mov	cx,(syskev - syskez) / 2; A rutin helym‚rete sz¢ban
	rep	movsw			; tm soljuk
	cmp	[versio],0500h		; Verzi¢sz m kisebb ?
	jc	kivema			; Igen, kisebb
	push	es			; Elmentem
	les	bx,dword ptr [bufcim]	; ES:BX <- pufferc¡m
	mov	ax,es:[bx + 10h]	; AX <- paragrafussz m
	pop	es			; ES vissza
	jmp	short mameva		; M r meg is van

kivema:
	int	12h			; Mem¢ria hossz k‚r‚s
	mov	cl,6			; L‚ptet‚si sz m
	shl	ax,cl			; AX <- paragrafussz m
mameva:
	mov	bx,ax			; M solat
	mov	word ptr [sysini - 4],ax; Ez a maximum
	push	cs			; Ebb”l lesz DS
	pop	ds			; DS <- CS
	inc	byte ptr [sysjel]	; Jelz” inkrement
	mov	[inijel],2		; M‚retsz m¡t s
	mov	ax,offset syskev - (syskez - xmskez); Itt kell kezd”denie
	add	ax,15			; Paragrafuson t£lra tol s
	mov	cl,4			; Ennyivel l‚ptetjk
	shr	ax,cl			; AX <- paragrafusc¡m
	mov	dx,cs			; DX <- saj t paragrafusom
	add	ax,dx			; Eltolom vele
	push	ax			; Ennyi a hossz
	mov	cx,16			; Most megszorzom ennyivel
	mul	cx			; DX:AX abszol£t c¡m
	mov	[extruc],ax		; C¡m L sz¢
	mov	[extruc + 2],dx 	; C¡m H sz¢
	pop	ax			; Hossz vissza
	mov	dx,[rammer]		; M‚ret
	test	dx,0fc00h		; Be vannak  llitva ?
	jne	tusobe			; Igen, t£l sok
	mov	cl,6			; Paragrafusra hatv ny
	shl	dx,cl			; Ennyi paragrafusban
	add	ax,dx			; Ennyi az ”sszes hossz
	jc	tusobe			; Ha annyi hely nincs
taenhe:
	mov	dx,word ptr [sysini - 4]; Maximum t r
	cmp	[versio],0500h		; A verzi¢ kisebb mint 5.00 ?
	jnc	vemaen			; Nem, hiba legyen
	sub	dx,3072 		; Ennyi marad ?
	jc	tusobe			; Nem, hiba
vemaen:
	cmp	ax,dx			; Maradt hely ?
	ja	tusobe			; Nem, ciki
	mov	[szacit],0		; Ez a marad‚k offszetje
	mov	[szacit + 2],ax 	; Ez a marad‚k szegmense
	clc				; Jelz” be ll¡t s
	ret				; Visszat‚r‚s
tusobe:
	push	cs			; Ebb”l lesz DS
	pop	ds			; DS <- CS
	mov	dx,offset ninele	; Sz”vegc¡m

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	String ki¡rat sa.				;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
strkir:
	cmp	byte ptr cs:[dumtip],0	; Cs”ndben ?
	jnz	csomst			; Vissza, cs”ndben vagyunk
	push	ds			; El fogom rontani
	push	cs			; Ebb”l lesz a DS
	pop	ds			; DS <- CS
	mov	ah,9			; Stringki¡rat s k¢dja
	int	21h			; DOS h¡v s
	pop	ds			; Eredeti DS vissza
csomst:
	stc				; Hibajelz”
	ret				; Visszat‚r‚s

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Rendszermem¢ria kezel”. 			;
;							;
;	Input:	DX:AX <- kezd” poz¡ci¢			;
;		BH    <- 0 --> olvas s			;
;		BH    <- 1 --> ¡r s			;
;		CX    <- sz¢sz m			;
;		ES:DI <- puffer 			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
syskez:
	add	ax,[extruc]		; Eltol s offszettel
	adc	dx,[extruc + 2] 	; Eltol s paragrafussal
	push	cx			; Sz¢sz m ment‚s
	mov	cx,16			; Paragrafushossz
	div	cx			; AX <- paragrafus eleje
	pop	cx			; Sz¢sz m vissza
	or	bh,bh			; Olvas s ?
	je	sysrdv			; Igen, nem lesz csere
	push	es			; Ebb”l lesz DS
	pop	ds			; DS <- ES
	mov	si,di			; Innen veheti
	mov	es,ax			; Ide tegye (paragrafus)
	mov	di,dx			; Ide tegye (offszet)
movvhs:
	rep	movsw			; tm soljuk
	clc				; Sikeres jelz”
	ret				; Visszat‚r‚s
sysrdv:
	mov	ds,ax			; Innen vegye (paragrafus)
	mov	si,dx			; Innen vegye (offszet)
	jmp	movvhs			; M sol sra

	if	($ - syskez) AND 1	; Ha a hossz p ratlan
syskev	equ	$ + 1			; Akkor egyel hosszabb
	else
syskev	equ	$			; Ha nem, akkor itt a v‚ge
	endif


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Ez a direktory label-je.			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

labelh	db	'KP-RAMDRIVE'           ; Ez a c¡mke neve
	db	8			; ATTR (label)
	db	10 dup(0)		; Reserved
	dw	0000000000000000b	; Id”
	datdef	<>			; Record kipakol s
	dw	0			; Cluster
	dw	0,0			; Hossz

secpuf	dw	512  dup(0)		; Szektor puffer

utmasc	label	byte			; Id ig fog m solni

emsnin	db	'RAMDRIVE: Expanded Memory Manager-t (EMS) nem tal lok !',0dh,0ah,'$'
emssta	db	'RAMDRIVE: Expanded Memory Manager (EMS) hiba !',0dh,0ah,'$'
xmsnot	db	'RAMDRIVE: Extended Memory Manager-t (XMS) nem tal lok !',0dh,0ah,'$'
noexme	db	'RAMDRIVE: Nincs extended mem¢ria !',0dh,0ah,'$'
badext	db	'RAMDRIVE: Hib s az Extended Memory Manager (XMS) parancs-l nc !',0dh,0ah,'$'
exmalh	db	'RAMDRIVE: Hiba az extended mem¢ria foglal sn l !',0dh,0ah,'$'
parhib	db	'RAMDRIVE: Helytelen param‚ter !',0dh,0ah,'$'
ninele	db	'RAMDRIVE: Kev‚s a mem¢ria !',0dh,0ah,'$'
iohiba	db	'RAMDRIVE: Hiba az inicializ l s sor n !',0dh,0ah,'$'
vershi	db	'RAMDRIVE: DOS verzi¢ nem megfelel”. (Nem 5.00 - 7.XX)',0dh,0ah,'$'
secadj	db	'RAMDRIVE: Helytelen param‚ter: szektor hossz m¢dos¡tva !',0dh,0ah,'$'

fejlec	db	0dh,0ah
	db	'Keresztes RAMDRIVE '
	vernum
	db	'-es verzi¢ '

drvsam	db	'A:',0dh,0ah,'$'
dsizsz	db	'    Diskm‚ret: $'
sectsz	db	'k',0dh,0ah
	db	'    Szektorhossz: $'
alluni	db	' byte',0dh,0ah
	db	'    Cluster szektorsz m: $'
entdar	db	' szektor',0dh,0ah
	db	'    K”nyvt rbejegyz‚sek sz ma: $'
crlfcs	db	0dh,0ah,'$'

kprdve	db	'KPRDV'                 ; Tulajdonos az EMSn‚l
	hndnum

dumtip: db	0			; Nincs duma jelz‚s, ha nem 0

	ifndef	 sysdef

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	A RAMDRIVE program v‚grehajtva. (COM program)	;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
start:
	mov	ah,0dh			; DISK alaphelyzetbe
	int	21h			; DOS h¡v s
	mov	ax,cs			; Itt tartok
	add	ax,10h			; Eltolom
	push	ax			; Ez lesz az £j CS
	mov	ax,offset folyta	; Itt lesz az igazi c¡m
	push	ax			; Az £j offszet
	retf				; A "folyta" c¡mre ugrok
folyta:
	cld				; N”vekv” stringmveletek
	mov	si,80h			; Ez a helye a param‚terle¡r¢nak
	lodsb				; Beolvasom
	or	al,al			; Nulla ?
	jne	vanval			; Nem, valamennyi van
helkar:
	mov	dx,offset helpsz	; Sz”vegc¡m
kirexi:
	call	strkir			; Stringki¡rat s
cshiki:
	mov	ax,4c01h		; Kil‚p‚s hib san k¢dja
	int	21h			; DOS h¡v s
vanval:
	call	utolso			; Z r¢karakter ?
	cmp	al,'z'                  ; Enn‚l nagyobb ?
	ja	helkar			; Igen, help ki¡rat s lesz
	cmp	al,'a'                  ; Enn‚l kisebb ?
	jae	vankar			; Nem, karakter
	cmp	al,'A'                  ; Enn‚l kisebb ?
	jb	helkar			; Igen, help ki¡rat s lesz
	cmp	al,'Z'                  ; Enn‚l nagyobb ?
	ja	helkar			; Igen, help ki¡rat s lesz
vankar:
	and	al,1fh			; Ez a lemezegys‚g k¢dja
	mov	dl,al			; DL <- lemezegys‚g k¢dja
	add	al,'@'                  ; Karakteres¡t‚s
	mov	cs:[drvsam],al		; Ez a drive karakteres alakja
	mov	cs:[csilla],al		; Ez a drive karakteres alakja
	lodsb				; Ez a k”vetkez” karakter
	cmp	al,':'                  ; Ez volt ?
	jnz	helkar			; Nem, help ki¡rat s lesz
	mov	ah,52h			; DOS v ltoz¢ c¡mk‚rdez‚s k¢dja
	int	21h			; DOS h¡v s
	lds	bx,es:[bx]		; Az els” DPB k¢dja
dpbell:
	cmp	bx,0ffffh		; Nincs t”bb ?
	jne	rencit			; De, nincs probl‚ma
	mov	dx,offset ervlem	; Sz”vegc¡m
kirext:
	jmp	short kirexi		; Hiba
rencit:
	dec	dl			; Ez a sz ml l¢ nulla ?
	je	megtal			; Igen, ez a DPB az
	lds	bx,[bx + 19h]		; Itt van a k”vetkez” DPB
	jmp	short dpbell		; Megn‚zzk ‚rv‚nyes-e
megtal:
	mov	al,[bx + 1]		; Ez az alegys‚gsz m
	mov	cs:[bpbolv + 1],al	; Alegys‚gsz m kit”lt‚s
	lds	bx,[bx + 13h]		; Itt van a DEVICE HEADER c¡me
	mov	cs:[blatad + 2],ds	; Ez a blokkfogad s paragrafusa
	mov	ax,[bx + 6]		; Ez a blokkfogad s offszetje
	mov	cs:[blatad],ax		; Elpakolom
	mov	cs:[feldol + 2],ds	; Ez a feldolgoz s paragrafusa
	mov	ax,[bx + 8]		; Ez a feldolgoz s offszetje
	mov	cs:[feldol],ax		; Elpkolom
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	push	cs			; Ebb”l lesz DS
	pop	ds			; DS <- CS
	mov	bx,offset bpbolv	; BPB c¡m k‚rdez‚s c¡me
	mov	[bx + 16],cs		; Puffer szegmense
	call	dword ptr [blatad]	; tadjuk a blokkot
	call	dword ptr [feldol]	; Feldolgoztatjuk
	mov	dx,offset hibdev	; Sz”vegc¡m
	mov	ax,[bx + 3]		; llapotjelz” sz¢
	or	ax,ax			; Volt hiba ?
	js	kirext			; Igen, hibaki¡rat s
	mov	es,[bx + 20]		; Ez a BPB paragrafusc¡me
	mov	di,offset jmptab	; Innen lehet ”sszehasonl¡tani
	push	si			; Elmentem a param‚terc¡met
	mov	si,di			; Ezzel lehet ”sszehasonl¡tani
	mov	cx,((xmskez - jmptab) - 1) / 2;Ennyi sz¢
	cli				; IT. most nem j”het
	rep	cmpsw			; ™sszehasonl¡t s
	sti				; Most m r h¡vhatja
	pop	si			; C¡m vissza
	mov	dx,offset nemsaj	; Sz”vegc¡m
	je	talsaj			; Ez a saj tom
kirexs:
	jmp	kirexi			; Ki¡rat s hib s kil‚p‚ssel
talsaj:
	mov	di,[bx + 18]		; Ez a BPB c¡me
	push	ss			; Ez a PSP c¡me
	pop	ds			; DS <- PSP paragrafusc¡me
	call	utolso			; Z r¢karakter ?
	je	igpaki			; Igen, param‚ter ki¡rat s
	push	ax			; Kell k‚s”bb
	push	si			; Kell k‚s”bb
	mov	ax,es:[di + 8]		; Ennyi szektor van az eszk”z”n
	mul	word ptr es:[di]	; Szektorm‚rettel szorz s
	mov	cx,1024 		; 1 Kbyte
	div	cx			; Ennyi Kbyte
	mov	cs:[rammer],ax		; Hossz
	mov	si,offset bpbofs	; Ez a saj tom
	mov	cx,10			; Ennyi a BPB hossza
masore:
	mov	ax,es:[di]		; Beolvasom
	mov	cs:[si],ax		; Kipakolom
	cmpsw				; DI <- DI + 2, SI <- SI + 2
	loop	masore			; R‚gi param‚terek m sol sa
	pop	si			; Vissza
	pop	ax			; Vissza
	jmp	nezaka			; Nem, van m‚g param‚ter
igpaki:
	push	di			; El fogom rontani
	mov	di,offset xmskez	; Ide pakoljuk
	mov	si,offset holtke	; Kezel” hiba
	mov	cx,(holtkv - holtke) / 2; Sz¢sz m
	cli				; IT. most nem j”het
	db	2eh			; CS el”tag
	rep	cmpsw			; Kezel” ”sszehasonl¡t s
	sti				; Most m r h¡vhatja
	pop	di			; C¡m vissza
	jne	erveke			; Ha nem a v‚ge van ott
	mov	dx,offset maneme	; Ki¡ratand¢ sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
erveke:
	mov	dx,offset fejlec	; Ki¡ratand¢ sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	dx,offset dsizsz	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,es:[di + 8]		; Ennyi szektor van az eszk”z”n
	mul	word ptr es:[di]	; Szektorm‚rettel szorz s
	mov	cx,1024 		; 1 Kbyte
	div	cx			; Ennyi Kbyte
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset sectsz	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,es:[di]		; Szektor byte-sz m
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset alluni	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	al,byte ptr es:[di + 2] ; Cluster-hossz
	xor	ah,ah			; H byte <- 0
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset entdar	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	ax,es:[di + 6]		; Bejegyz‚ssz m
	call	decoax			; AX ki¡rat s decim lisan
	mov	dx,offset crlfcs	; Sz”vegc¡m
	call	strkir			; Ki¡rat s DX szerint
	mov	dx,offset sysdev	; Sz”vegc¡m
	mov	al,es:[drvtip]		; Ez a tipus
	and	al,3			; El‚g ez a k‚t bit
	cmp	al,2			; System ?
	je	strkip			; Igen, ki¡ratjuk
	mov	dx,offset emsdev	; Sz”vegc¡m
	dec	al			; Expanded ?
	je	strkip			; Igen, ki¡ratjuk
	mov	dx,offset xmsdev	; Sz”vegc¡m
strkip:
	call	strkir			; Ki¡rat s DX szerint
kiloke:
	mov	ax,4c00h		; Kil‚p‚s sikeresen k¢dja
	int	21h			; DOS h¡v s
nezaka:
	mov	dx,offset sysnem	; Sz”vegc¡m
	test	es:[drvtip],2		; System ?
	je	nesifo			; Nem, mehet
	jmp	kirexi			; Igen, az nem lehet
nesifo:
	cmp	al,'/'                  ; Kapcsol¢ ?
	jne	konviz			; Ez nem, tal n konstans
	lodsb				; Beolvassuk
	test	cs:[drvtip],2		; Volt m r ?
	jne	mehkar			; Nem, elemezzk
hivahe:
	cmp	al,'Q'                  ; Cs”nd ?
	je	dumoff			; Igen, az lesz
	cmp	al,'q'                  ; Cs”nd ?
	je	dumoff			; Igen, az lesz
	jmp	helkar			; Help ki¡rat s
mehkar:
	cmp	al,'E'                  ; Extended ?
	je	exjeva			; Igen, az lesz
	cmp	al,'e'                  ; Extended ?
	jne	expest			; Nem, esetleg Axpanded
exjeva:
	mov	cs:[drvtip],0		; Extended jelz‚s
	jmp	short jeleli		; Jelz‚s ut nra
expest:
	cmp	al,'A'                  ; Axpanded ?
	je	expjva			; Igen, az lesz
	cmp	al,'a'                  ; Axpanded ?
	je	expjva			; Igen, az lesz
	cmp	al,'O'                  ; Kikapcsol s
	je	expoff			; Igen, az lesz
	cmp	al,'o'                  ; Kikapcsol s
	je	expoff			; Igen, az lesz
	cmp	al,'Q'                  ; Cs”nd ?
	je	dumoff			; Igen, az lesz
	cmp	al,'q'                  ; Cs”nd ?
	je	dumoff			; Igen, az lesz
	jne	hivahe			; Igen, az lesz
dumoff:
	mov	byte ptr cs:[dumtip],0ffh; Nincs duma jelz‚s
	jmp	short jeleli		; Jelz‚s ut nra
expoff:
	mov	cs:[drvtip],0ffh	; Kikapcsol s jelz‚s
	jmp	short jeleli		; Jelz‚s ut nra
expjva:
	mov	cs:[drvtip],1		; Axpanded jelz‚s
jeleli:
	call	utolso			; Van m‚g ?
	jne	nesifo			; Igen, van m‚g
	jmp	utocat			; Nincs t”bb
konviz:
	dec	si			; Vissza a sz mjegyre
	call	conbeo			; Konstans beolvas s
	cmp	cs:[konsam],3		; Harmadik ut n ?
	ja	hivahe			; Igen, hiba
	je	hakobe			; Ha az, mehet
	cmp	cs:[konsam],2		; M sodik ?
	je	szemem			; Igen, szektorm‚ret
	cmp	bx,4			; Enn‚l nagyobb ?
	jc	hivahe			; Nem, hiba
	cmp	bx,32768		; Nagyobb ?
	jnc	hivahe			; Nagyobb, hiba
	mov	cs:[rammer],bx		; M‚ret
	jmp	short szainm		; Sz ml l¢ n”vel‚sre
szemem:
	mov	cs:[bpbofs],bx		; Uj m‚ret
	mov	dx,cs:[rammer]		; M‚ret
	mov	al,6			; Ez a kezd” hatv ny
	cmp	bx,128			; Ennyi ? (2**6 sz¢)
	jne	nekmfb			; Nem, k”vetkez”re
	cmp	dx,8192 		; 8 Kbyte-n l kisebb ?
	jc	takesm			; Igen, ¡gy meg is felel
	shl	bx,1			; Dupl zzuk
nekmfb:
	inc	al			; Hatv ny n”vel‚s
	cmp	bx,256			; Ennyi ? (2**7 sz¢)
	jne	nekmeb			; Nem, k”vetkez”re
	cmp	dx,16384		; 16 Kbyte-n l kisebb ?
	jc	takesm			; Igen, ¡gy meg is felel
	shl	bx,1			; Dupl zzuk
nekmeb:
	inc	al			; Hatv ny n”vel‚s
	cmp	bx,512			; Ennyi ? (2**8 sz¢)
	je	takesm			; Igen, ¡gy meg is felel
hivahs:
	jmp	helkar			; Help ki¡rat s
takesm:
	mov	byte ptr cs:[bpbofs + 19],al; Hatv nysz m ment‚s
	cmp	bx,cs:[bpbofs]		; Azonos a tervezettel ?
	je	szainm			; Igen, sz ml l¢ n”vel‚sre
	mov	cs:[bpbofs],bx		; Ez lesz az igazi
	mov	dx,offset secadj	; Sz”vegc¡m
	call	strkir			; Ki¡ratjuk
	jmp	short szainm		; Sz ml l¢ n”vel‚sre
hakobe:
	cmp	bx,2			; Ennyi van ?
	jc	hivahs			; Ha nincs hiba
	cmp	bx,1024 		; Enn‚l nagyobb
	ja	hivahs			; Igen, ez hiba
	mov	di,cs:[bpbofs]		; Szektor byte-sz m
	mov	cl,5			; L‚ptet‚ssz m
	shr	di,cl			; Ennyi bejegyz‚s f‚r
	mov	ax,bx			; AX <- konstans
	xor	dx,dx			; DX <- 0
	div	di			; Ennyi bejegyz‚s lehet
	or	dx,dx			; Marad‚k 0 ?
	je	nekasb			; Igen, ennyi bejegyz‚s lesz
	sub	di,dx			; Ennyi maradna
	add	bx,di			; Megn”veljk annyival
nekasb:
	mov	cs:[bpbofs + 6],bx	; Bejegyz‚ssz m
szainm:
	inc	cs:[konsam]		; Konstans sz ml l¢ ++
	jmp	jeleli			; Tov bbi param‚terre
utocat:
	mov	dx,offset syslet	; Sz”vegc¡m
	cmp	cs:[drvtip],2		; System ?
	jne	nesile			; Nem, mehet a csere-bere
bajled:
	jmp	kirexi			; Igen, hibajelz‚s
nesile:
	push	cs			; Ebb”l lesz DS
	pop	ds			; DS <- CS
	test	es:[drvtip],80h 	; Kikapcsolt ?
	jne	nekerf			; Igen, nem k‚rdezzk meg
	mov	ah,19h			; Aktu lis egys‚g k‚rdez‚s k¢dja
	int	21h			; DOS h¡v s
	add	al,'A'                  ; Karakteres¡t‚s
	mov	dx,offset akttil	; Sz”vegc¡m
	mov	si,offset csilla	; Ez a gy”k‚r le¡r¢
	cmp	al,[si] 		; Ez az aktu lis ?
	je	bajled			; Ha ez az aktu lis is
	mov	dx,si			; Ez a gy”k‚r le¡r¢
	mov	cx,0ffffh		; Attributum
	mov	ah,4eh			; Els” hivatkoz s k¢dja
kokere:
	int	21h			; DOS h¡v s
	jc	lehehi			; Ha hib val futott
	mov	ax,4f00h		; Ez a tov bbkeres‚s k¢dja
	test	byte ptr ss:[80h + 21],8; Label ?
	jne	kokere			; Igen, tov bbkeres‚s
lehehi:
	mov	dx,offset fogdrv	; Sz”vegc¡m
	cmp	al,2			; File not found k¢dja ?
	je	nekerf			; Igen, nincs rajta
	cmp	al,18			; Nincs t”bb file k¢dja ?
	jne	bajled			; Nem, hiba
nekerf:
	mov	di,offset xmskez	; Ide pakoljuk
	mov	si,offset holtke	; Kezel” hiba
	mov	cx,(holtkv - holtke) / 2; Sz¢sz m
	cli				; IT. most nem j”het
	mov	es:[csekod],0		; Jelz‚s, hogy cser‚lt‚k
	rep	movsw			; Kezel”  tm sol s
	sti				; Most m r h¡vhatja
	or	es:[drvtip],80h 	; Jelenleg ki van kapcsolva
	mov	ax,0ffffh		; Nem volt handle k¢dja
	mov	dx,ax			; M solat
	xchg	dx,es:[xmshnd]		; ?MS handle
	test	es:[drvtip],3		; EMS ?
	je	xmsrem			; Nem, jelenleg XMS
	cmp	dx,ax			; Volt EMS handle ?
	je	emsnhn			; Nem, m‚g nem volt
	mov	ah,45h			; Close k¢dja
	int	67h			; EMS kezel” h¡v s
emsnhn:
	cmp	[drvtip],0ffh		; Kikapcsol s legyen ?
	je	emskik			; Igen, pr¢b ljuk meg
	cmp	[drvtip],1		; EMS legyen ? (Az is volt !)
	je	remoks			; Igen, nem kell EMS remove
emskik:
	mov	ax,0ffffh		; Jelz‚s, hogy nincs
	cmp	es:[xmsrut],ax		; Nincs vektor 1. ?
	jne	kekika			; De van !
	cmp	es:[xmsrut + 2],ax	; Nincs vektor 2. ?
remoks:
	je	remoke			; Igen, mehet m sik is
kekika:
	push	ds			; Elmentjk
	push	es			; Elmentjk
	push	es			; Ebb”l lesz DS
	mov	dx,es			; Ezzel kell ”sszehasonl¡tani
	mov	ax,3519h		; Ez a vektor most
	int	21h			; DOS h¡v s
	pop	ds			; DS <- ES
	mov	ax,es			; Ez a paragrafus
	cmp	ax,dx			; Azonosak ?
	mov	dx,offset vekelt	; Sz”vegc¡m
	je	segazo			; Igen, offszetet is kell !
kirexm:
	cmp	cs:[drvtip],0ffh	; Kikapcsol s ?
	je	remokj			; Igen, kikapcsoljuk
	jmp	kirexi			; Igen, hibajelz‚s
segazo:
	cmp	bx,offset emsk19 - (emskez - xmskez); Ez a kezel” c¡me
	jne	kirexm			; Hiba
	call	tikivi			; 19-es vektor helyre ll¡t s
	jmp	short remokr		; 19-es REMOVE rendben
xmsrem:
	cmp	dx,ax			; Volt XMS handle ?
	je	nevhnm			; Nem, m‚g nem volt
	mov	ah,0dh			; XMS r”gz¡t‚s elenged‚s k¢dja
	call	dword ptr es:[xmsrut]	; XMS kezel”
	mov	ah,0ah			; XMS visszaad s k¢dja
	call	dword ptr es:[xmsrut]	; XMS kezel”
nevhnm:
	cmp	[drvtip],0ffh		; Kikapcsol s legyen ?
	je	xmskik			; Igen, pr¢b ljuk meg
	cmp	[drvtip],0		; XMS legyen ? (Az is volt !)
	je	remoke			; Igen, nem kell a remove
xmskik:
	mov	ax,0ffffh		; Nincs kezel” ?
	cmp	es:[jmppos],ax		; Volt kezel” m r ?
	jne	tavokm			; Igen, volt m r
	cmp	es:[jmppos + 2],ax	; Volt kezel” m r ?
	je	remoke			; Nem, eddig m‚g nem
tavokm:
	mov	dx,offset vekelx	; Sz”vegc¡m
	cmp	word ptr es:[hikehe],03ebh; Short jmp k¢dja van ?
	jne	kirexm			; Nem lehet vissza¡rni
	push	ds			; Elmentjk
	push	es			; Elmentjk
	push	es			; Ebb”l lesz DS
	pop	ds			; DS <- ES
	call	nevhnd			; Handle vissza¡r s
remokr:
	or	es:[drvtip],40h 	; Jelenleg remove van
remokj:
	pop	es			; Vissza
	pop	ds			; Vissza
remoke:
	cmp	[drvtip],0ffh		; Kikapcsol s ?
	jne	drvtiv			; Nem, t¡pusvizsg lat
	jmp	kiloke			; Kil‚p‚s sikeresen
drvtiv:
	cmp	[drvtip],1		; EMS ?
	jne	xmskei			; Nem, XMS lesz
	mov	si,offset emskez	; Saj t rutin c¡me
	mov	di,offset xmskez	; Kezel” fejc¡m
	mov	cx,(emskev - emskez) / 2; A rutin helym‚rete sz¢ban
	cli				; IT. ne j”jj”n
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	push	es			; Elromlana
	push	cs			; Ebb”l lesz ES
	pop	es			; ES <- CS
	mov	si,offset emskez	; Saj t rutin c¡me
	mov	di,offset xmskez	; Kezel” fejc¡m
	mov	cx,(emskev - emskez) / 2; A rutin helym‚rete sz¢ban
	cli				; IT. ne j”jj”n
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	call	emsope			; EMS nyit s
	pop	es			; ES vissza
	jnc	siopke			; EMS sikeres nyit s (OK handle)
nsixop:
	jmp	cshiki			; Kil‚p‚s hib val
siopke:
	push	es			; Elrontja a rutin
	push	cs			; Ez lesz a c¡m
	pop	ds			; DS <- CS
	call	drvini			; Drive inicializ l s
	pop	es			; ES vissza
	jnc	sikine			; Ha sikerlt inicializ lni
	mov	dx,0ffffh		; Ez legyen helyette
	xchg	dx,[xmshnd]		; Ezt kell megszntetni
	mov	ah,45h			; Close k¢dja
	int	67h			; EMS kezel” h¡v s
xmsnoi:
	mov	dx,offset ninele	; Sz”vegc¡m
	jmp	kirexi			; Igen, hibajelz‚s
sikine:
	push	es			; Elrontja
	test	es:[drvtip],40h 	; Remove volt ?
	jne	remoem			; Igen, nem probl‚ma
	les	bx,dword ptr es:[xmsrut]; Ez az eredeti vektor
	mov	[xmsrut],bx		; Offszet elpakol s
	mov	[xmsrut + 2],es 	; Paragrafus elpakol s
	jmp	short emsvol		; Igen, nem probl‚ma
remoem:
	mov	ax,3519h		; 19-es vektor lek‚rdez‚s
	int	21h			; DOS h¡v s
	mov	[xmsrut],bx		; Offszet elpakol s
	mov	[xmsrut + 2],es 	; Paragrafus elpakol s
	pop	es			; ES vissza
	push	es			; ES vissza
	push	ds			; Ez a CS is
	push	es			; Ebb”l lesz DS
	pop	ds			; DS <- ES
	mov	dx,offset emsk19 - (emskez - xmskez); Ez a kezel” c¡me
	mov	ax,2519h		; 19-es vektor m¢dos¡t s
	int	21h			; DOS h¡v s
	pop	ds			; DS <- CS
emsvol:
	pop	es			; ES vissza
	mov	es:[drvtip],1		; EMS lett
	jmp	inikes			; Befejezve
xmskei:
	mov	si,offset xmskez	; Saj t rutin c¡me
	mov	di,si			; Kezel” fejc¡m
	mov	cx,(xmskev - xmskez) / 2; A rutin helym‚rete sz¢ban
	cli				; IT. ne j”jj”n
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	call	xmsope			; XMS handle nyit s
	jc	nsixop			; Ha nem sikeres
	test	es:[drvtip],40h 	; Remove volt ?
	jne	remoxm			; Igen, nem probl‚ma
	push	es			; El fogja rontani
	les	bx,dword ptr es:[jmppos]; Ez az eredeti vektor
	mov	[jmppos],bx		; Offszet elpakol s
	mov	[jmppos + 2],es 	; Paragrafus elpakol s
	pop	es			; ES vissza
	push	es			; El fogja rontani
	les	bx,dword ptr es:[extruc]; Ez az eredeti vektor
	mov	[extruc],bx		; Offszet elpakol s
	mov	[extruc + 2],es 	; Paragrafus elpakol s
	pop	es			; ES vissza
	jmp	short xmsvol		; Igen, nem probl‚ma
remoxm:
	mov	si,offset hikehe	; Saj t rutin c¡me
	mov	di,si			; Kezel” fejc¡m
	mov	cx,(xmskev - hikehe) / 2; A rutin helym‚rete sz¢ban
	cli				; IT. ne j”jj”n
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	mov	[bpbpar],es		; Ez legyen a kezel” c¡me
	push	es			; Elromlik
	call	extrui			; XMS kezel” inicializ l s
	pop	es			; ES vissza
	jnc	xmsvol			; Ha sikerlt
	mov	dx,0ffffh		; Ez legyen helyette
	xchg	dx,[xmshnd]		; Ezt kell megszntetni
	mov	ah,0dh			; XMS r”gz¡t‚s elenged‚s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	mov	ah,0ah			; XMS visszaad s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	jmp	cshiki			; Kil‚p‚s hib val
xmsvol:
	mov	si,offset xmsrut	; Param‚terek c¡me
	mov	di,si			; Param‚terek c¡me
	mov	cx,7			; A BPB helym‚rete sz¢ban
	cli				; IT. most nem j”het
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	push	es			; El fogja rontani
	call	drvini			; Drive inicializ l s
	pop	es			; ES vissza
	jnc	sikikx			; Ha sikerlt inicializ lni
	mov	dx,0ffffh		; Ez legyen helyette
	xchg	dx,[xmshnd]		; Ezt kell megszntetni
	mov	ah,0dh			; XMS r”gz¡t‚s elenged‚s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	mov	ah,0ah			; XMS visszaad s k¢dja
	call	dword ptr [xmsrut]	; XMS kezel”
	test	es:[drvtip],40h 	; Remove volt ?
	je	neremo			; Hibajelz‚sre
	push	es			; Elmentjk
	call	nevhnd			; Handle vissza¡r s
	pop	es			; Vissza
neremo:
	jmp	xmsnoi			; Ha nem sikeres
sikikx:
	mov	es:[drvtip],0		; XMS lett
inikes:
	mov	si,offset bpbofs	; Saj t BPB c¡me
	mov	di,si			; A kezel” BPB c¡me
	mov	cx,10			; A BPB helym‚rete sz¢ban
	cli				; IT. most nem j”het
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	mov	si,offset xmsrut	; Param‚terek c¡me
	mov	di,si			; Param‚terek c¡me
	mov	cx,7			; A BPB helym‚rete sz¢ban
	cli				; IT. most nem j”het
	rep	movsw			; tm soljuk
	sti				; Most m r j”het
	jmp	kiloke			; Kil‚p‚s sikeresen

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Utols¢ karakter vizsg lat			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
holtke:
	mov	cs:[csekod],0		; Jelz‚s, hogy cser‚lt‚k
	mov	al,7			; Hibak¢d
	stc				; Hibajel
	ret				; Visszat‚r‚s

	if	($ - holtke) AND 1	; Ha a hossz p ratlan
holtkv	equ	$ + 1			; Akkor egyel hosszabb
	else
holtkv	equ	$			; Ha nem, akkor itt a v‚ge
	endif

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;							;
;	Utols¢ karakter vizsg lat			;
;							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
utolso:
	lodsb				; Parancskarakter beolvas s
	cmp	al,' '                  ; Sz¢k”z ?
	je	utolso			; Igen, £j karakter beolvas s
	cmp	al,9			; Tabul tor ?
	je	utolso			; Igen, £j karakter beolvas s
	cmp	al,0dh			; CR ?
	je	igretr			; Igen, visszt‚r‚s (string v‚ge)
	cmp	al,0ah			; LF ?
	je	igretr			; Igen, visszt‚r‚s (string v‚ge)
	or	al,al			; Nulla ?
igretr:
	ret				; Visszat‚r‚s

bpbolv	db	22			; Hossz
	db	0			; Alegys‚g
	db	2			; BPB c¡m k‚rdez‚s k¢dja
	dw	0			; llapotjelz” sz¢
	db	8 dup(0)		; DOS fenntartott terlet
	db	0			; Jelenlegi t¡pus
	dw	secpuf,0		; Pointer a pufferre
	dw	0,0			; Pointer a BPB-re

blatad	dw	0,0			; Ez a blokk tad s c¡me
feldol	dw	0,0			; Ez a feldolgoz s c¡me


helpsz	db	'RAMDRIVE param‚ter m¢dos¡t¢ program. ('
	vernum
	db	')',0dh,0ah,0ah
	db	'Haszn lata: KPRDV @:                                    '
	db	'              vagy',0dh,0ah
	db	'            KPRDV @: <kapcsol¢(k)>                      '
	db	'              vagy',0dh,0ah
	db	'            KPRDV @: hossz <kapcsol¢(k)>                '
	db	'              vagy',0dh,0ah
	db	'            KPRDV @: hossz szektorhossz <kapcsol¢(k)>   '
	db	'              vagy',0dh,0ah
	db	'            KPRDV @: hossz szektorhossz bejegyz‚ssz m'
	db	' <kapcsol¢(k)>',0dh,0ah,0ah
	db	'A @: a m¢dos¡tand¢ RAMDRIVE azonos¡t¢ja.',0dh,0ah,0ah
	db	'Alap‚rtelmez‚sek:  Hossz:          64 Kbyte. (4...32768)',0dh,0ah
	db	'                   Szektorhossz:  512 byte.  (128...512)',0dh,0ah
	db	'                   Bejegyz‚ssz m:  64 darab. (2....1024)',0dh,0ah,0ah
	db	'A kapcsol¢ lehet : /O -> RAMDRIVE megszntet‚s.',0dh,0ah
	db	'                   /E -> Extended mem¢ria legyen.',0dh,0ah
	db	'                   /A -> Expanded mem¢ria legyen.',0dh,0ah,0ah
	db	'                   /Q -> Ne ¡rjon ki zenetet.',0dh,0ah,0ah
	db	'Ha csak a @: szerepel, a RAMDRIVE param‚tereit '
	db	'list zza ki.',0dh,0ah,'$'

fogdrv	db	'A lemez nem res !',0ah,0dh,'$'

ervlem	db	'rv‚nytelen lemezhivatkoz s !',0ah,0dh,'$'

hibdev	db	'Hib s eszk”zkezel‚s, kil‚pek !',0ah,0dh,'$'

nemsaj	db	'Ez nem a saj t RAMDRIVE kezel”m !',0ah,0dh,'$'

akttil	db	'Nem lehet aktu lis drive !',0ah,0dh,'$'

xmsdev	db	'Extended Memory Manager (XMS) kezel”.',0dh,0ah,'$'

emsdev	db	'Expanded Memory Manager (EMS) kezel”.',0dh,0ah,'$'

sysdev	db	'System mem¢ria. Ez nem szntethet” meg, nem m¢dos¡that¢ !',0dh,0ah,'$'

sysnem	db	'System mem¢riakezel”t nem lehet m¢dos¡tani !',0dh,0ah,'$'

syslet	db	'System mem¢riakezel”t nem lehet l‚trehozni !',0dh,0ah,'$'

vekelt	db	'Nem lehet helyre ll¡tani a 19-es IT. vektort !',0dh,0ah
	db	'A RAMDRIVE kezel” (EMS) most nem m¢dos¡that¢.',0dh,0ah,'$'

vekelx	db	'Nem lehet helyre ll¡tani az XMS parancs-l ncot !',0dh,0ah
	db	'A RAMDRIVE kezel” (XMS) most nem m¢dos¡that¢.',0dh,0ah,'$'

maneme	db	0ah,0dh,'A RAMDRIVE jeleneleg nem zemel !',0dh,0ah
	db	'El”z” param‚terei:',0dh,0ah,'$'

csilla	db	'?:\*.*',0

	endif

	code	ends
	end
